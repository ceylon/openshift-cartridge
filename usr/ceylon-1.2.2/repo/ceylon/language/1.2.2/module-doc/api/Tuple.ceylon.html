<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Tuple.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;&quot;&quot;A _tuple_ is a typed linked list. Each instance of 
   `Tuple` represents the value and type of a single link.
   The attributes `first` and `rest` allow us to retrieve a 
   value from the list without losing its static type 
   information.
   
       value point = Tuple(0.0, Tuple(0.0, Tuple(&quot;origin&quot;)));
       Float x = point.first;
       Float y = point.rest.first;
       String label = point.rest.rest.first;
   
   Usually, we abbreviate code involving tuples.
   
       [Float,Float,String] point = [0.0, 0.0, &quot;origin&quot;];
       Float x = point[0];
       Float y = point[1];
       String label = point[2];
   
   A list of types enclosed in brackets is an abbreviated 
   tuple type. An instance of `Tuple` may be constructed by 
   surrounding a value list in brackets:
   
       [String,String] words = [&quot;hello&quot;, &quot;world&quot;];
   
   The index operator with a literal integer argument is a 
   shortcut for a chain of evaluations of `rest` and 
   `first`. For example, `point[1]` means `point.rest.first`.
   
   A _terminated_ tuple type is a tuple where the type of
   the last link in the chain is `Empty`. An _unterminated_ 
   tuple type is a tuple where the type of the last link
   in the chain is `Sequence` or `Sequential`. Thus, a 
   terminated tuple type has a length that is known
   statically. For an unterminated tuple type only a lower
   bound on its length is known statically.
   
   Here, `point` is an unterminated tuple:
   
       String[] labels = ... ;
       [Float,Float,String*] point = [0.0, 0.0, *labels];
       Float x = point[0];
       Float y = point[1];
       String? firstLabel = point[2];
       String[] allLabels = point[2...];&quot;&quot;&quot;
by (&quot;Gavin&quot;)
tagged(&quot;Sequences&quot;, &quot;Basic types&quot;, &quot;Collections&quot;)
shared final serializable
native class Tuple&lt;out Element, out First, out Rest = []&gt;
        (first, rest)
        extends Object()
        satisfies [Element+]
        given First satisfies Element
        given Rest satisfies Element[] {
    
    &quot;The first element of this tuple. (The head of the 
     linked list.)&quot;
    shared actual native 
    First first;
    
    &quot;A tuple with the elements of this tuple, except for the
     first element. (The tail of the linked list.)&quot;
    shared actual native 
    Rest rest;
    
    shared actual native 
    Integer lastIndex =&gt; rest.size;
    
    shared actual native 
    Integer size =&gt; 1 + rest.size;
    
    shared actual native 
    Element? getFromFirst(Integer index) 
            =&gt; switch (index &lt;=&gt; 0)
            case (smaller) null
            case (equal) first
            case (larger) rest.getFromFirst(index - 1);
    
    &quot;The last element of this tuple.&quot;
    shared actual native 
    Element last 
            =&gt; if (nonempty rest)
            then rest.last
            else first;
    
    shared actual native 
    Element[] measure(Integer from, Integer length) {
        if (length &lt;= 0) {
            return [];
        }
        Integer realFrom = from &lt; 0 then 0 else from;
        if (realFrom == 0) {
            return length == 1 
                    then [first]
                    else rest[0 : length + realFrom - 1]
                            .withLeading(first);
        }
        return rest[realFrom - 1 : length];
    }
    
    shared actual native 
    Element[] span(Integer from, Integer end) {
        if (from &lt; 0 &amp;&amp; end &lt; 0) { return []; }
        Integer realFrom = from &lt; 0 then 0 else from;
        Integer realEnd = end &lt; 0 then 0 else end;
        return realFrom &lt;= realEnd 
            then this[from : realEnd - realFrom + 1]
            else this[realEnd : realFrom - realEnd + 1]
                        .reversed.sequence();
    }
    
    shared actual native 
    Element[] spanTo(Integer to)
            =&gt; to &lt; 0 then [] else span(0, to);
    
    shared actual native 
    Element[] spanFrom(Integer from)
            =&gt; span(from, size);
    
    &quot;This tuple.&quot;
    shared actual native 
    Tuple&lt;Element,First,Rest&gt; clone() =&gt; this;
    
    shared actual native 
    Iterator&lt;Element&gt; iterator() 
            =&gt; object
            satisfies Iterator&lt;Element&gt; {
        variable Element[] current = outer;
        shared actual Element|Finished next() {
            if (nonempty c = current) {
                current = c.rest;
                return c.first;
            }
            else {
                return finished;
            }
        }
        string =&gt; &quot;``outer.string``.iterator()&quot;;
    };
    
    &quot;Determine if the given value is an element of this
     tuple.&quot;
    shared actual native 
    Boolean contains(Object element) 
            =&gt; if (exists first, first == element)
            then true
            else element in rest;
    
    &quot;Return a new tuple that starts with the specified
     [[element]], followed by the elements of this tuple.&quot;
    shared actual native
    Tuple&lt;Element|Other,Other,Tuple&lt;Element,First,Rest&gt;&gt;
    withLeading&lt;Other&gt;(
            &quot;The first element of the resulting tuple.&quot;
            Other element)
            =&gt; Tuple(element, this);
    
    &quot;Return a new tuple containing the elements of this 
     tuple, followed by the given [[element]].&quot;
    shared actual native
    [First,Element|Other+] withTrailing&lt;Other&gt;(
            &quot;The last element of the resulting tuple.&quot;
            Other element) 
            =&gt; Tuple(first, rest.withTrailing(element));
    
    &quot;Return a tuple containing the elements of this 
     tuple, followed by the given [[elements]].&quot;
    shared actual native
    [First,Element|Other*] append&lt;Other&gt;(
            &quot;The list of elements to be appended.&quot;
            Other[] elements)
            =&gt; Tuple(first, rest.append(elements));
}
</pre></body></html>