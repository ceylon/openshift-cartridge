<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>formatFloat.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;The string decimal representation of the given 
 [[floating point number|float]]. If the given number is 
 [[negative|Float.negative]], the string representation will 
 begin with `-`. The [[whole part|Float.wholePart]] and 
 [[fractional parts|Float.fractionalPart]] of the number are
 separated by a `.` decimal point. Digits consist of decimal 
 digits `0` to `9`. 
 
 The number of decimal places following the decimal point is 
 controlled by the parameters [[minDecimalPlaces]] and 
 [[maxDecimalPlaces]], which default to `1` and `9` 
 respectively, so that by default the string representation
 always contains a decimal point, and never contains more 
 than nine decimal places. The decimal representation is 
 truncated so that the number of decimal places never 
 exceeds the specified maximum.
 
 For example:
 
 - `formatFloat(1234.1234)` is `\&quot;1234.1234\&quot;`
 - `formatFloat(0.1234)` is `\&quot;0.1234\&quot;`
 - `formatFloat(1234.0)` is `\&quot;1234.0\&quot;`
 - `formatFloat(1234.0,0)` is `\&quot;1234\&quot;`
 - `formatFloat(1234.1234,6)` is `\&quot;1234.123400\&quot;`
 - `formatFloat(1234.1234,0,2)` is `\&quot;1234.12\&quot;`
 - `formatFloat(0.0001,2,2)` is `\&quot;0.00\&quot;`
 - `formatFloat(0.0001,0,2)` is `\&quot;0\&quot;`
 
 Finally:
 
 - `formatFloat(-0.0)` is `\&quot;0.0\&quot;`,
 - `formatFloat(0.0/0)` is `\&quot;NaN\&quot;`,
 - `formatFloat(1.0/0)` is `\&quot;Infinity\&quot;`, and
 - `formatFloat(-1.0/0)` is `\&quot;-Infinity\&quot;.`
 
 This function never produces a representation involving 
 scientific notation.&quot;
tagged(&quot;Numbers&quot;)
see (`function formatInteger`,
     `function parseFloat`)
shared String formatFloat(
        &quot;The floating point value to format.&quot;
        Float float,
        &quot;The minimum number of allowed decimal places.
         
         If `minDecimalPlaces&lt;=0`, the result may have no
         decimal point.&quot;
        Integer minDecimalPlaces=1,
        &quot;The maximum number of allowed decimal places.
         
         If `maxDecimalPlaces&lt;=0`, the result always has no
         decimal point.&quot;
        Integer maxDecimalPlaces=9) {
    if (float.undefined || float.infinite) {
        return float.string;
    }
    Float magnitude = float.magnitude;
    variable Integer i = maxDecimalPlaces;
    Float maxExactIntegralFloat 
            = runtime.maxExactIntegralFloat.float;
    while (i&gt;0 &amp;&amp; 
        magnitude &gt; maxExactIntegralFloat/10^i) {
        //reduce the number of computed decimal places, 
        //so that magnitude * 10^i will fit into a Float
        i--;
    }
    Integer decimalPlaces = i;
    variable Boolean previousZero = false;
    variable {Character*} digits = {};
    while (true) {
        Float scaled = scaleByPowerOfTen(magnitude, i);
        Float fractional = scaled.fractionalPart;
        Float whole = scaled.wholePart;
        Integer d = (fractional * 10).integer;
        Integer digit;
        if (previousZero) {
            digit = 
                    //detect rounding error in floating point
                    //multiplication magnitude * 10^i above
                    (fractional * 100).integer &gt; d * 10
                        then (d==9 then 0 else d + 1)
                        else d;
        }
        else {
            digit = d;
        }
        Character c = (digit + zeroInt).character;
        digits = digits.follow(c);
        if (whole == 0.0) {
            break;
        }
        previousZero = digit==0;
        i--;
    }
    String string = String(digits.exceptLast);
    Integer point = string.size - decimalPlaces;
    String wholePart;
    String fractionalPart;
    if (point&gt;0) {
        wholePart = string[0:point];
        fractionalPart = string[point...];
    }
    else {
        wholePart = &quot;0&quot;; 
        fractionalPart 
                = string.padLeading(maxDecimalPlaces, &apos;0&apos;);
    }
    
    String normalized = 
            fractionalPart
                .trimTrailing(&apos;0&apos;.equals)
                .padTrailing(minDecimalPlaces, &apos;0&apos;);
    String signed = 
            float.negative
                then &quot;-&quot; + wholePart
                else wholePart;
    return normalized.empty 
            then signed 
            else signed + &quot;.&quot; + normalized;
}

Float scaleByPowerOfTen(Float float, Integer power) {
    Float scale =
            let (magnitude = power.magnitude)
            //there are only 15 decimal places of precision
            //in IEEE double-precision floating point number 
            if (magnitude &lt;= 15)
                then (10^magnitude).nearestFloat     //fast
                else 10.0.powerOfInteger(magnitude); //slow
    return if (power &lt; 0) 
            then float / scale 
            else float * scale;
}
</pre></body></html>