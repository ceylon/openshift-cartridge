<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>ClassOrInterfaceDeclaration.ceylon</title><link href='../../.resources/favicon.ico' rel='shortcut icon'/>
<link href='../../.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='../../.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '../../.resources/'</script><script src='../../.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='../../.resources/rainbow.min.js' type='text/javascript'></script><script src='../../.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='../../.resources/ceylon.js' type='text/javascript'></script><script src='../../.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>import ceylon.language { AnnotationType = Annotation }
import ceylon.language.meta.model {
    ClassOrInterface,
    Member,
    AppliedType = Type,
    IncompatibleTypeException,
    TypeApplicationException
}

&quot;&quot;&quot;A class or interface declaration.
   
   &lt;a name=&quot;toplevel-sample&quot;&gt;&lt;/a&gt;
   ### Usage sample for toplevel classes
   
   Because some classes have type parameters, getting a model requires applying type arguments to the
   class declaration with [[apply]] in order to be able to instantiate that class. For example, here is how you would
   obtain a class or interface model that you can instantiate from a toplevel class declaration:
   
       class Foo&lt;T&gt;() {
           string =&gt; &quot;Hello, our T is: ``typeLiteral&lt;T&gt;()``&quot;;
       }
       
       void test(){
           // We need to apply the Integer closed type to the Foo declaration in order to get the Foo&lt;Integer&gt; closed type
           ClassOrInterface&lt;Foo&lt;Integer&gt;&gt; classOrInterfaceModel = `class Foo`.apply&lt;Foo&lt;Integer&gt;&gt;(`Integer`);
           assert(is Class&lt;Foo&lt;Integer&gt;,[]&gt; classOrInterfaceModel);
           // This will print: Hello, our T is: ceylon.language::Integer
           print(classOrInterfaceModel());
       }
   
   Note that there are more specialised versions of [[apply]] in [[ClassDeclaration.classApply]] and 
   [[InterfaceDeclaration.interfaceApply]].

   &lt;a name=&quot;member-sample&quot;&gt;&lt;/a&gt;
   ### Usage sample for member classes
	
   For member classes or interfaces it is a bit longer, because member types need to be applied not only their type arguments but also
   the containing type, so you should use [[memberApply]] and start by giving the containing closed type:
   
       class Outer(){
           shared class Inner(){
               string =&gt; &quot;Hello&quot;;
           }
       }
	
       void test(){
           // apply the containing closed type `Outer` to the member class declaration `Outer.Inner`
           value memberClassModel = `class Outer.Inner`.memberApply&lt;Outer,Outer.Inner&gt;(`Outer`);
           assert(is MemberClass&lt;Outer,Outer.Inner,[]&gt; memberClassModel);
           // We now have a MemberClass, which needs to be applied to a containing instance in order to become an
           // invokable class model:
           Class&lt;Outer.Inner,[]&gt; boundMemberClassModel = memberClassModel(Outer());
           // This will print: Hello
           print(boundMemberClassModel());
       }
   
   Note that there are more specialised versions of [[memberApply]] in [[ClassDeclaration.memberClassApply]] and 
   [[InterfaceDeclaration.memberInterfaceApply]].
   &quot;&quot;&quot;
shared sealed interface ClassOrInterfaceDeclaration 
        of ClassDeclaration | InterfaceDeclaration 
        satisfies NestableDeclaration &amp; GenericDeclaration {
    
    &quot;This type&apos;s extended type, unless this is the class for 
     [[Anything|ceylon.language::Anything]], whichis the root 
     of the type hierarchy and thus does not have any extended type.&quot;
    shared formal OpenClassType? extendedType;
    
    &quot;The list of types satisfied by this type.&quot;
    shared formal OpenInterfaceType[] satisfiedTypes;
    
    &quot;If this type has an `of` clause, this is the list of case types for the current type.&quot;
    shared formal OpenType[] caseTypes;
    
    &quot;True if this type is an alias type, in which case the [[extendedType]] will 
     contain the substituted type.&quot;
    shared formal Boolean isAlias;
    
    // FIXME: should Kind default to NestableDeclaration?
    &quot;Returns the list of shared member declarations that satisfy the 
     given `Kind` type argument. 
     This includes inherited declarations but not unshared declarations.&quot;
    shared formal Kind[] memberDeclarations&lt;Kind&gt;() 
        given Kind satisfies NestableDeclaration;

    &quot;Returns the list of member declarations directly declared on this class or interface, 
     which satisfy the given `Kind` type argument. 
     This includes unshared declarations but not inherited declarations.&quot;
    shared formal Kind[] declaredMemberDeclarations&lt;Kind&gt;() 
        given Kind satisfies NestableDeclaration;

    &quot;Returns the list of shared member declarations that satisfy the given `Kind` type argument and
     that are annotated with the given `Annotation` type argument. This includes inherited
     declarations but not unshared declarations.&quot;
    shared formal Kind[] annotatedMemberDeclarations&lt;Kind, Annotation&gt;() 
        given Kind satisfies NestableDeclaration
        given Annotation satisfies AnnotationType;
    
    &quot;Returns the list of member declarations directly declared on this class or interface, which satisfy the given 
     `Kind` type argument and that are annotated with the given `Annotation` type argument.
     This includes unshared declarations but not inherited declarations.&quot;
    shared formal Kind[] annotatedDeclaredMemberDeclarations&lt;Kind, Annotation&gt;() 
        given Kind satisfies NestableDeclaration
        given Annotation satisfies AnnotationType;
    
    &quot;Looks up a shared member declaration by name, 
     provided it satisfies the given `Kind` type argument. 
     Returns `null` if no such member matches. 
     This includes inherited declarations but not unshared declarations&quot;
    shared formal Kind? getMemberDeclaration&lt;Kind&gt;(String name) 
        given Kind satisfies NestableDeclaration;

    &quot;Looks up a member declaration directly declared on this class or interface, by name, 
     provided it satisfies the given `Kind` type argument. 
     Returns `null` if no such member matches. 
     This includes unshared declarations but not inherited declarations.&quot;
    shared formal Kind? getDeclaredMemberDeclaration&lt;Kind&gt;(String name) 
        given Kind satisfies NestableDeclaration;
    
    &quot;Applies the given closed type arguments to this toplevel class or interface declaration in order to obtain a class or interface model. 
     See [this code sample](#toplevel-sample) for an example on how to use this.&quot;
    throws(`class IncompatibleTypeException`, 
        &quot;If the specified `Type` type argument is not compatible with the actual result.&quot;)
    throws(`class TypeApplicationException`, 
            &quot;If the specified closed type argument values are not compatible 
             with the actual result&apos;s type parameters.&quot;)
    shared formal ClassOrInterface&lt;Type&gt; apply&lt;Type=Anything&gt;(AppliedType&lt;&gt;* typeArguments);
    
    &quot;Applies the given closed container type and type arguments to this member class or interface declaration in order to obtain a 
     member class or interface model. See [this code sample](#member-sample) for an example on how to use this.&quot;
    throws(`class IncompatibleTypeException`, 
        &quot;If the specified `Container` or `Type` type arguments are not 
         compatible with the actual result.&quot;)
    throws(`class TypeApplicationException`, 
            &quot;If the specified closed container type or type argument values 
             are not compatible with the actual result&apos;s container type or 
             type parameters.&quot;)
    shared formal Member&lt;Container, ClassOrInterface&lt;Type&gt;&gt; &amp; ClassOrInterface&lt;Type&gt; memberApply
            &lt;Container=Nothing, Type=Anything&gt;
            (AppliedType&lt;Object&gt; containerType, AppliedType&lt;&gt;* typeArguments);

}
</pre></body></html>