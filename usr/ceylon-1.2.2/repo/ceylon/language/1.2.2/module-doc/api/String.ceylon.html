<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>String.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;&quot;&quot;A string of characters. Each character in the string is a 
   [[32-bit Unicode character|Character]]. The internal 
   UTF-16 encoding is hidden from clients.
   
   Literal strings may be written between double quotes:
   
       &quot;hello world&quot;
       &quot;\r\n&quot;
       &quot;\{#03C0} \{#2248} 3.14159&quot;
       &quot;\{GREEK SMALL LETTER PI} \{ALMOST EQUAL TO} 3.14159&quot;
   
   Alternatively, a _verbatim string_ may be written between
   tripled double quotes.
   
   The _empty string_, `&quot;&quot;`, is a string with no characters.
   
   A string is a [[Category]] of its [[characters]], and of 
   its substrings:
   
       &apos;w&apos; in greeting 
       &quot;hello&quot; in greeting
   
   Strings are [[summable|Summable]]:
   
       String greeting = &quot;hello&quot; + &quot; &quot; + &quot;world&quot;;
   
   They are efficiently [[iterable|Iterable]]:
   
       for (char in &quot;hello world&quot;) { ... }
   
   They are [[lists|List]] of [[characters|Character]]:
   
       value char = &quot;hello world&quot;[5];
   
   They are [[ranged|Ranged]]:
   
       String who = &quot;hello world&quot;[6...];
   
   Note that since `string[index]` evaluates to the optional 
   type `Character?`, it is often more convenient to write 
   `string[index..index]`, which evaluates to a `String` 
   containing a single character, or to the empty string 
   `&quot;&quot;` if `index` refers to a position outside the string.
   
   It is easy to use comprehensions to transform strings:
   
       String { for (s in &quot;hello world&quot;) if (s.letter) s.uppercased }
   
   Since a `String` has an underlying UTF-16 encoding, 
   certain operations are expensive, requiring iteration of 
   the characters of the string. In particular, [[size]]
   requires iteration of the whole string, and `get()`,
   `span()`, and `measure()` require iteration from the 
   beginning of the string to the given index.&quot;&quot;&quot;
by (&quot;Gavin&quot;)
tagged(&quot;Basic types&quot;, &quot;Strings&quot;)
shared native final class String(characters)
        extends Object()
        satisfies SearchableList&lt;Character&gt; &amp;
                  Comparable&lt;String&gt; &amp;
                  Summable&lt;String&gt; &amp; 
                  Ranged&lt;Integer,Character,String&gt; {
    
    &quot;The characters that form this string.&quot;
    {Character*} characters;
    
    &quot;This string, with all characters in lowercase.
     
     Conversion of uppercase characters to lowercase is
     performed according to a locale-independent mapping
     that produces incorrect results in certain locales
     (e.g. `tr-TR`).
     
     The resulting string may not have the same number of
     characters as this string, since the uppercase 
     representation of certain characters comprises multiple
     characters, for example the lowercase representation of 
     \{LATIN CAPITAL LETTER I WITH DOT ABOVE} is two 
     characters wide.&quot;
    shared native String lowercased;
    
    &quot;This string, with all characters in uppercase.
     
     Conversion of lowercase characters to uppercase is
     performed according to a locale-independent mapping
     that produces incorrect results in certain locales
     (e.g. `tr-TR`).
     
     The resulting string may not have the same number of
     characters as this string, since the uppercase 
     representation of certain characters comprises multiple
     characters, for example the uppercase representation of 
     \{LATIN SMALL LETTER SHARP S} is SS.&quot;
    shared native String uppercased;
    
    &quot;Split the string into tokens, using the given 
     [[predicate function|splitting]] to determine which 
     characters are separator characters.
     
         value pathElements = path.split(&apos;/&apos;.equals);
     
     The flags [[discardSeparators]] and [[groupSeparators]]
     determine how separator characters should occur in the
     resulting stream.
     
     Note that for the case of the empty string, `split()` 
     always produces a stream containing a single empty 
     token. For example:
     
         \&quot;\&quot;.split(&apos;/&apos;.equals)
     
     evaluates to the nonempty stream `{ \&quot;\&quot; }`.&quot;
    shared native {String+} split(
            &quot;A predicate that determines if a character is a
             separator characters at which to split. Default 
             to split at any 
             [[whitespace|Character.whitespace]] character.&quot;
            Boolean splitting(Character ch) =&gt; ch.whitespace,
            &quot;Specifies that the separator characters
             occurring in the string should be discarded. If 
             `false`, they will be included in the resulting 
             iterator.&quot;
            Boolean discardSeparators = true,
            &quot;Specifies that the separator tokens should be 
             grouped eagerly and not be treated as 
             single-character tokens. If `false` each 
             separator token will be of size `1`.&quot;
            Boolean groupSeparators = true);
    
    &quot;The first character in the string.&quot;
    shared actual native Character? first;
    
    &quot;The last character in the string.&quot;
    shared actual native Character? last;
    
    &quot;The rest of the string, without its first character.&quot;
    shared actual native String rest;
    
    //&quot;Get the character at the specified index, or `null` if
    // the index falls outside the bounds of this string.&quot;
    //shared actual native Character? get(Integer index);
    
    &quot;Get the character at the specified index, where the 
     string is indexed from the _end_ of the string, or 
     `null` if the index falls outside the bounds of this 
     string.&quot;
    shared actual native Character? getFromLast(Integer index);
    
    &quot;A sequence containing all indexes of this string.&quot;
    shared actual native Integer[] keys =&gt; 0:size;
    
    &quot;Join the [[string representations|Object.string]] of 
     the given [[objects]], using this string as a separator.&quot;
    shared native String join({Object*} objects);
    
    &quot;Split the string into lines of text, discarding line
     breaks. Recognized line break sequences are `\\n` and 
     `\\r\\n`.&quot;
    see (`value linesWithBreaks`)
    shared native {String*} lines 
            =&gt; split(&apos;\n&apos;.equals, true, false)
               .spread(String.trimTrailing)(&apos;\r&apos;.equals);
    
    &quot;Split the string into lines of text with line breaks.
     Each line will be terminated by a line break sequence,
     `\\n` or `\\r\\n`.&quot;
    see (`value lines`)
    shared native {String*} linesWithBreaks
            =&gt; split(&apos;\n&apos;.equals, false, false)
            .partition(2)
            .map((lineWithBreak)
                    =&gt; let (line = lineWithBreak[0], 
                            br = lineWithBreak[1])
                    if (exists br) then line+br else line);
    
    &quot;A string containing the characters of this string, 
     after discarding [[whitespace|Character.whitespace]] 
     from the beginning and end of the string.&quot;
    shared native String trimmed =&gt; trim(Character.whitespace);
    
    &quot;A string containing the characters of this string, 
     after discarding the characters matching the given 
     [[predicate function|trimming]] from the beginning and 
     end of the string.
     
         value trimmed = name.trim(&apos;_&apos;.equals);
     
     A character is removed from the string if it matches
     the given predicate and if either:
     
     - every character occurring earlier in the string also 
       matches the predicate, or
     - every character occurring later in the string also
       matches the predicate.&quot;
    shared actual native String trim(
            &quot;The predicate function that determines whether
             a character should be trimmed&quot;
            Boolean trimming(Character element));
    
    &quot;A string containing the characters of this string, 
     after discarding the characters matching the given 
     [[predicate function|trimming]] from the 
     beginning of the string.
     
     A character is removed from the string if it matches
     the given predicate and every character occurring 
     earlier in the string also matches the predicate.&quot;
    shared actual native String trimLeading(
            &quot;The predicate function that determines whether
             a character should be trimmed&quot;
            Boolean trimming(Character element));
    
    &quot;A string containing the characters of this string, 
     after discarding the characters matching the given 
     [[predicate function|trimming]] from the end of the 
     string.
     
     A character is removed from the string if it matches
     the given predicate and every character occurring 
     later in the string also matches the predicate.&quot;
    shared actual native String trimTrailing(
            &quot;The predicate function that determines whether
             a character should be trimmed&quot;
            Boolean trimming(Character element));

    &quot;A string containing the characters of this string after 
     collapsing strings of [[whitespace|Character.whitespace]] 
     into single space characters and discarding whitespace 
     from the beginning and end of the string.&quot;
    shared native String normalized;
    
    &quot;A string containing the characters of this string, with
     the characters in reverse order.&quot;
    shared native actual String reversed;
    
    &quot;Determines if this string contains a character at the
     given [[index]], that is, if `0&lt;=index&lt;size`.&quot;
    shared native actual Boolean defines(Integer index);
    
    &quot;A string containing the characters of this string 
     between the given indexes. If the [[start index|from]] 
     is the same as the [[end index|to]], return a string 
     with a single character. If the start index is larger 
     than the end index, return the characters in the 
     reverse order from the order in which they appear in 
     this string. If both the start index and the end index 
     are larger than the last index in the string, or if 
     both the start index and the end index are smaller than
     the first index in the string, return the empty string. 
     Otherwise, if the last index is larger than the last 
     index in the string, return all characters from the 
     start index to last character of the string.&quot;
    shared actual native String span(Integer from, Integer to);
    
    &quot;A string containing the characters of this string from 
     the given [[start index|from]] inclusive to the end of 
     the string. If the start index is larger than the last 
     index of the string, return the empty string. If the
     start index is negative, return this string.&quot;
    shared actual native String spanFrom(Integer from)
            =&gt; from&lt;size then span(from, size) else &quot;&quot;;
    
    &quot;A string containing the characters of this string from 
     the start of the string up to and including the given 
     [[end index|to]]. If the end index is negative, return 
     the empty string. If the end index is larger than the
     last index in this string, return this string.&quot;
    shared actual native String spanTo(Integer to)
            =&gt; to&gt;=0 then span(0, to) else &quot;&quot;;
    
    &quot;A string containing the characters of this string 
     beginning at the given [[start index|from]], returning 
     a string no longer than the given [[length]]. If the 
     portion of this string starting at the given index is 
     shorter than the given length, return the portion of 
     this string from the given index until the end of this 
     string. Otherwise, return a string of the given length. 
     If the start index is larger than the last index of the 
     string, return the empty string.&quot;
    shared native actual String measure(Integer from, 
                                        Integer length);
    
    &quot;Select the first characters of this string, returning a 
     string no longer than the given [[length]]. If this 
     string is shorter than the given length, return this 
     string. Otherwise, return a string of the given length.&quot;
    shared native actual String initial(Integer length);
    
    &quot;Select the last characters of the string, returning a 
     string no longer than the given [[length]]. If this 
     string is shorter than the given length, return this 
     string. Otherwise, return a string of the given length.&quot;
    shared native actual String terminal(Integer length);
    
    &quot;Return two strings, the first containing the characters
     that occur before the given [[index]], the second with
     the characters that occur after the given `index`. If 
     the given `index` is outside the range of indices of 
     this string, one of the returned strings will be empty.&quot;
    shared native actual [String,String] slice(Integer index);
    
    &quot;The length of the string (the number of characters it 
     contains). In the case of the empty string, the string 
     has length zero. Note that this operation is 
     potentially costly for long strings, since the
     underlying representation of the characters uses a
     UTF-16 encoding. Use of [[longerThan]] or 
     [[shorterThan]] is highly recommended.&quot;
    see (`function longerThan`, `function shorterThan`)
    shared actual native Integer size;
    
    &quot;The index of the last character in the string, or 
     `null` if the string has no characters. Note that this 
     operation is potentially costly for long strings, since 
     the underlying representation of the characters uses a 
     UTF-16 encoding. For any nonempty string:
     
         string.lastIndex == string.size-1&quot;
    shared actual Integer? lastIndex 
            =&gt; if (size==0) then null else size-1;
    
    &quot;An iterator for the characters of the string.&quot;
    shared actual native Iterator&lt;Character&gt; iterator();
    
    &quot;Returns the character at the given [[index]] in the 
     string, or `null` if the index is before the start of 
     the string or past the end of string. The first 
     character in the string occurs at index zero. The last 
     character in the string occurs at index 
     `string.size-1`.&quot;
    shared actual native Character? getFromFirst(Integer index);
    
    //shared actual native Boolean-&gt;Character? lookup(Integer index);
    
    &quot;Determines if the given object is a `String` and, if 
     so, if it occurs as a substring of this string, or if 
     the object is a `Character` that occurs in this string. 
     That is to say, a string is considered a [[Category]] 
     of its substrings and of its characters.&quot;
    shared actual native Boolean contains(Object element);
    
    shared actual native Boolean startsWith(List&lt;Anything&gt; substring);
    
    shared actual native Boolean endsWith(List&lt;Anything&gt; substring);
    
    &quot;Returns the concatenation of this string with the
     given string.&quot;
    shared actual native String plus(String other);
    
    &quot;Returns a string formed by repeating this string the 
     given number of [[times]], or the empty string if
     `times&lt;=0`.&quot;
    shared actual native String repeat(Integer times);
    
    &quot;Returns a string formed by replacing every occurrence 
     in this string of the given [[substring]] with the 
     given [[replacement]] string, working from the start of
     this string to the end.&quot;
    shared native String replace(String substring, 
                                 String replacement);
    
    &quot;Returns a string formed by replacing the first 
     occurrence in this string of the given [[substring]], 
     if any, with the given [[replacement]] string.&quot;
    shared native String replaceFirst(String substring, 
                                      String replacement);
    
    &quot;Returns a string formed by replacing the last 
     occurrence in this string of the given [[substring]], 
     if any, with the given [[replacement]] string.&quot;
    shared native String replaceLast(String substring, 
                                     String replacement);
    
    &quot;Compare this string with the given string 
     lexicographically, according to the Unicode code points
     of the characters.
     
     This defines a locale-independent collation that is
     incorrect in some locales.&quot;
    shared actual native Comparison compare(String other) {
        value min = smallest(size, other.size);
        for (i in 0:min) {
            assert (exists thisChar = this.getFromFirst(i),
                    exists thatChar = other.getFromFirst(i));
            if (thisChar!=thatChar) {
                return thisChar &lt;=&gt; thatChar;
            }
        }
        return size &lt;=&gt; other.size;
    }
    
    &quot;Compare this string with the given string 
     lexicographically, ignoring the case of the characters.
     That is, by considering two characters `x` and `y` as
     equal if:
     
     - `x == y`,
     - `x.uppercased == y.uppercased`, or
     - `x.lowercased == y.lowercased`.
     
     This defines a locale-independent collation that is
     incorrect in some locales.&quot;
    see (`value Character.lowercased`, 
         `value Character.uppercased`)
    shared native Comparison compareIgnoringCase(String other) {
        value min = smallest(size, other.size);
        for (i in 0:min) {
            assert (exists thisChar = this.getFromFirst(i),
                    exists thatChar = other.getFromFirst(i));
            if (thisChar!=thatChar &amp;&amp; 
                thisChar.uppercased!=thatChar.uppercased &amp;&amp;
                thisChar.lowercased!=thatChar.lowercased) {
                return thisChar.lowercased &lt;=&gt; thatChar.lowercased;
            }
        }
        return size &lt;=&gt; other.size;
    }
    
    &quot;Determines if this string is longer than the given
     [[length]]. This is a more efficient operation than
     `string.size&gt;length`.&quot;
    see (`value size`)
    shared actual native Boolean longerThan(Integer length);
    
    &quot;Determines if this string is shorter than the given
     [[length]]. This is a more efficient operation than
     `string.size&gt;length`.&quot;
    see (`value size`)
    shared actual native Boolean shorterThan(Integer length);
    
    &quot;Determines if the given object is a `String`, and if 
     so, if this string has the same [[length|size]], and 
     the same [[characters]], in the same order, as the 
     given [[string|that]].&quot;
    shared actual native Boolean equals(Object that) {
        if (is String that) {
            if (size!=that.size) {
                return false;
            }
            value min = smallest(size, that.size);
            for (i in 0:min) {
                assert (exists thisChar = this.getFromFirst(i),
                        exists thatChar = that.getFromFirst(i));
                if (thisChar!=thatChar) {
                    return false;
                }
            }
            return true;
        }
        else {
            return false;
        }
    }
    
    &quot;Compare this string with the given string, ignoring the 
     case of the characters. That is, by considering two 
     characters `x` and `y` as equal if:
     
     - `x == y`,
     - `x.uppercased == y.uppercased`, or
     - `x.lowercased == y.lowercased`.&quot;
    see (`value Character.lowercased`, 
         `value Character.uppercased`)
    shared native Boolean equalsIgnoringCase(String that) {
        if (size!=that.size) {
            return false;
        }
        value min = smallest(size, that.size);
        for (i in 0:min) {
            assert (exists thisChar = this.getFromFirst(i),
                    exists thatChar = that.getFromFirst(i));
            if (thisChar!=thatChar &amp;&amp; 
                thisChar.uppercased!=thatChar.uppercased &amp;&amp;
                thisChar.lowercased!=thatChar.lowercased) {
                return false;
            }
        }
        return true;
    }
    
    &quot;A hash code for this `String`, computed from its
     UTF-16 code units.&quot;
    shared actual native Integer hash;
    
    &quot;This string.&quot;
    shared actual String string =&gt; this;
    
    &quot;Determines if this string has no characters, that is, 
     if it has zero [[size]]. This is a _much_ more 
     efficient operation than `string.size==0`.&quot;
    see (`value size`)
    shared actual native Boolean empty;
    
    &quot;This string.&quot;
    shared actual String coalesced =&gt; this;
    
    &quot;This string.&quot;
    shared actual String clone() =&gt; this;
    
    &quot;Pad this string with the given [[character]], producing 
     a string of the given minimum [[size]], centering the
     string.&quot;
    shared native String pad(Integer size, 
        &quot;The padding character&quot;
        Character character=&apos; &apos;);
    
    &quot;Left pad this string with the given [[character]], 
     producing a string of the given minimum [[size]].&quot;
    shared native String padLeading(Integer size, 
        &quot;The padding character&quot;
        Character character=&apos; &apos;);
    
    &quot;Right pad this string with the given [[character]], 
     producing a string of the given minimum [[size]].&quot;
    shared native String padTrailing(Integer size, 
        &quot;The padding character&quot;
        Character character=&apos; &apos;);
    
    &quot;Efficiently copy the characters in the segment
     `sourcePosition:length` of this string to the segment 
     `destinationPosition:length` of the given 
     [[character array|destination]].&quot;
    shared native 
    void copyTo(
        &quot;The array into which to copy the elements.&quot;
        Array&lt;Character&gt; destination,
        &quot;The index of the first element in this array to 
         copy.&quot;
        Integer sourcePosition = 0,
        &quot;The index in the given array into which to copy the 
         first element.&quot;
        Integer destinationPosition = 0,
        &quot;The number of elements to copy.&quot;
        Integer length 
                = smallest(size - sourcePosition,
                    destination.size - destinationPosition));
    
    shared actual native List&lt;Character&gt; sublistFrom(Integer from);
    shared actual native List&lt;Character&gt; sublistTo(Integer to);
    
    shared actual native {Integer*} indexesWhere(Boolean selecting(Character element));
    shared actual native Integer? firstIndexWhere(Boolean selecting(Character element));
    shared actual native Integer? lastIndexWhere(Boolean selecting(Character element));
    
    shared actual native {Integer*} occurrences(Character element, Integer from, Integer length);
    shared actual native {Integer*} inclusions(List&lt;Character&gt; sublist, Integer from);
    
    shared actual native Boolean occurs(Character element, Integer from, Integer length);
    shared actual native Boolean occursAt(Integer index, Character element);
    shared actual native Boolean includes(List&lt;Character&gt; sublist, Integer from);
    shared actual native Boolean includesAt(Integer index, List&lt;Character&gt; sublist);
        
    shared actual native Integer? firstOccurrence(Character element, Integer from, Integer length);
    shared actual native Integer? lastOccurrence(Character element, Integer from, Integer length);
    shared actual native Integer? firstInclusion(List&lt;Character&gt; sublist, Integer from);
    shared actual native Integer? lastInclusion(List&lt;Character&gt; sublist, Integer from);
        
    shared actual native Boolean largerThan(String other); 
    shared actual native Boolean smallerThan(String other); 
    shared actual native Boolean notSmallerThan(String other); 
    shared actual native Boolean notLargerThan(String other);
    
    shared actual native void each(void step(Character element));
    shared actual native Integer count(Boolean selecting(Character element));
    shared actual native Boolean every(Boolean selecting(Character element));
    shared actual native Boolean any(Boolean selecting(Character element));
    shared actual native Result|Character|Null reduce&lt;Result&gt;
            (Result accumulating(Result|Character partial, Character element));
    shared actual native Character? find(Boolean selecting(Character element));
    shared actual native Character? findLast(Boolean selecting(Character element));
    shared actual native &lt;Integer-&gt;Character&gt;? locate(Boolean selecting(Character element));
    shared actual native &lt;Integer-&gt;Character&gt;? locateLast(Boolean selecting(Character element));
    shared actual native {&lt;Integer-&gt;Character&gt;*} locations(Boolean selecting(Character element));
}
</pre></body></html>