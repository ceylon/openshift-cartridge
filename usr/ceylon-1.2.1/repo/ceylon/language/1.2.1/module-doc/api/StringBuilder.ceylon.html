<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>StringBuilder.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>import java.lang {
    JStringBuilder=StringBuilder,
    JCharacter=Character {
        toChars,
        charCount
    }
}

&quot;&quot;&quot;Builder utility for constructing [[strings|String]] by 
   incrementally appending strings or characters.
   
       value builder = StringBuilder();
       builder.append(&quot;hello&quot;);
       builder.appendCharacter(&apos; &apos;);
       builder.append(&quot;world&quot;);
       String hello = builder.string; //hello world&quot;&quot;&quot;
tagged(&quot;Strings&quot;)
shared native final class StringBuilder() 
        satisfies List&lt;Character&gt; {
    
    &quot;The number characters in the current content, that is, 
     the [[size|String.size]] of the produced [[string]].&quot;
    shared actual native Integer size;
    
    shared actual native Integer? lastIndex;
    
    &quot;The resulting string. If no characters have been
     appended, the empty string.&quot;
    shared actual native String string;
    
    shared actual native Iterator&lt;Character&gt; iterator();
    
    &quot;Returns a string of the given [[length]] containing
     the characters beginning at the given [[index]].&quot;
    shared native 
    String substring(Integer index, Integer length);
    
    shared actual native
    Character? getFromFirst(Integer index);
    
    &quot;Append the characters in the given [[string]].&quot;
    shared native 
    StringBuilder append(String string);
    
    &quot;Append the characters in the given [[strings]].&quot;
    shared native 
    StringBuilder appendAll({String*} strings) {
        for (s in strings) {
            append(s);
        }
        return this;
    }
    
    &quot;Prepend the characters in the given [[string]].&quot;
    shared native 
    StringBuilder prepend(String string);
    
    &quot;Prepend the characters in the given [[strings]].&quot;
    shared native 
    StringBuilder prependAll({String*} strings) {
        for (s in strings) {
            prepend(s);
        }
        return this;
    }
    
    &quot;Append the given [[character]].&quot;
    shared native 
    StringBuilder appendCharacter(Character character);
    
    &quot;Prepend the given [[character]].&quot;
    shared native 
    StringBuilder prependCharacter(Character character);
    
    &quot;Append a newline character.&quot;
    shared native 
    StringBuilder appendNewline() =&gt; appendCharacter(&apos;\n&apos;);
    
    &quot;Append a space character.&quot;
    shared native 
    StringBuilder appendSpace() =&gt; appendCharacter(&apos; &apos;);
    
    &quot;Remove all content and return to initial state.&quot;
    shared native 
    StringBuilder clear();
    
    &quot;Insert a [[string]] at the specified [[index]].&quot;
    shared native 
    StringBuilder insert(Integer index, String string);
    
    &quot;Insert a [[character]] at the specified [[index]].&quot;
    shared native 
    StringBuilder insertCharacter
    (Integer index, Character character) ;
    
    &quot;Replaces the specified [[number of characters|length]] 
     from the current content, starting at the specified 
     [[index]], with the given [[string]]. If `length` is 
     nonpositive, nothing is replaced.&quot;
    shared native 
    StringBuilder replace
    (Integer index, Integer length, String string);
    
    &quot;Deletes the specified [[number of characters|length]] 
     from the current content, starting at the specified 
     [[index]]. If `length` is nonpositive, nothing is 
     deleted.&quot;
    shared native 
    StringBuilder delete(Integer index, Integer length/*=1*/);
    
    &quot;Deletes the specified [[number of characters|length]] 
     from the start of the string. If `length` is 
     nonpositive, nothing is deleted.&quot;
    shared native 
    StringBuilder deleteInitial(Integer length);
    
    &quot;Deletes the specified [[number of characters|length]] 
     from the end of the string. If `length` is nonpositive, 
     nothing is deleted.&quot;
    shared native 
    StringBuilder deleteTerminal(Integer length);
    
    &quot;Reverses the order of the current characters.&quot;
    shared native 
    StringBuilder reverseInPlace();
    
    shared actual native Boolean equals(Object that);
    shared actual native Integer hash;
}

shared native(&quot;jvm&quot;) final class StringBuilder() 
        satisfies List&lt;Character&gt; {
    
    value builder = JStringBuilder();
    
    shared actual native(&quot;jvm&quot;) Integer size 
            =&gt; builder.codePointCount(0, builder.length());
    
    shared actual native(&quot;jvm&quot;) Integer? lastIndex 
            =&gt; if (builder.length() == 0)
            then null
            else builder.length() - 1;
    
    shared actual native(&quot;jvm&quot;) String string 
            =&gt; builder.string;
    
    shared actual native(&quot;jvm&quot;) 
    Iterator&lt;Character&gt; iterator() {
        object stringBuilderIterator
                satisfies Iterator&lt;Character&gt; {
            variable Integer offset = 0;
            shared actual Character|Finished next() {
                if (offset &lt; builder.length()) {
                    Integer codePoint = 
                            builder.codePointAt(offset);
                    offset += charCount(codePoint);
                    return codePoint.character;
                }
                else {
                    return finished;
                }
            }
        }
        return stringBuilderIterator;
    }
    
    shared native(&quot;jvm&quot;) 
    String substring(Integer index, Integer length) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        &quot;index+length must not be greater than size&quot;
        assert (index+length&lt;=size);
        if (length&gt;0) {
            Integer start = startIndex(index);
            Integer end = endIndex(start, length);
            return builder.substring(start, end);
        }
        else {
            return &quot;&quot;;
        }
    }
    
    shared actual native(&quot;jvm&quot;)
    Character? getFromFirst(Integer index) 
            =&gt; if (index&lt;0 || index&gt;size)
            then null
            else builder.codePointAt(startIndex(index)).character;
    
    shared native(&quot;jvm&quot;) 
    StringBuilder append(String string) {
        builder.append(string);
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder prepend(String string) {
        builder.insert(0, string);
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder appendCharacter(Character character) {
        builder.appendCodePoint(character.integer);
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder prependCharacter(Character character) {
        builder.insert(0, toChars(character.integer));
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder clear() {
        builder.setLength(0);
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder insert(Integer index, String string) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        builder.insert(startIndex(index), string);
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder insertCharacter
            (Integer index, Character character) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        builder.insert(startIndex(index),
            toChars(character.integer));
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder replace
            (Integer index, Integer length, String string) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        &quot;index+length must not be greater than size&quot;
        assert (index+length&lt;=size);
        if (!string.empty) {
            Integer len = (length&lt;0) then 0 else length;
            Integer start = startIndex(index);
            Integer end = endIndex(start, len);
            builder.replace(start, end, string);
        }
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder delete(Integer index, Integer length) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        &quot;index+length must not be greater than size&quot;
        assert (index+length&lt;=size);
        if (length&gt;0) {
            Integer start = startIndex(index);
            Integer end = endIndex(start, length);
            builder.delete(start, end);
        }
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder deleteInitial(Integer length) {
        &quot;length must not be greater than size&quot;
        assert (length&lt;=size);
        if (length&gt;0) {
            builder.delete(0, startIndex(length));
        }
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder deleteTerminal(Integer length) {
        &quot;length must not be greater than size&quot;
        assert (length&lt;=size);
        if (length&gt;0) {
            Integer start = startIndex(size - length);
            builder.delete(start, builder.length());
        }
        return this;
    }
    
    shared native(&quot;jvm&quot;) 
    StringBuilder reverseInPlace() {
        builder.reverse();
        return this;
    }
    
    shared actual native(&quot;jvm&quot;) Boolean equals(Object that) 
            =&gt; builder.equals(that);
    
    shared actual native(&quot;jvm&quot;) Integer hash 
            =&gt; builder.hash;
    
    Integer startIndex(Integer index) 
            =&gt; builder.offsetByCodePoints(0, index);
    
    Integer endIndex(Integer start, Integer length) 
            =&gt; builder.offsetByCodePoints(start, length);
    
}

shared native(&quot;js&quot;) final class StringBuilder() 
        satisfies List&lt;Character&gt; {
    
    variable String str = &quot;&quot;;
    
    shared actual native(&quot;js&quot;) Integer size =&gt; str.size;
    
    shared actual native(&quot;js&quot;) Integer? lastIndex 
            =&gt; if (str.size == 0)
            then null
            else str.size - 1;
    
    shared actual native(&quot;js&quot;) String string =&gt; str;
    
    shared actual native(&quot;js&quot;) 
    Iterator&lt;Character&gt; iterator() 
            =&gt; str.iterator();
    
    shared native(&quot;js&quot;) 
    String substring(Integer index, Integer length) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        &quot;index+length must not be greater than size&quot;
        assert (index+length&lt;=size);
        return if (length&gt;0) then str[index..length] else &quot;&quot;;
    }
    
    shared actual native(&quot;js&quot;)
    Character? getFromFirst(Integer index) 
            =&gt; if (index&lt;0 || index&gt;size) 
            then null 
            else str.getFromFirst(index);
    
    shared native(&quot;js&quot;) 
    StringBuilder append(String string) {
        str = str + string;
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder prepend(String string) {
        str = string + str;
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder appendCharacter(Character character) {
        str = str + character.string;
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder prependCharacter(Character character) {
        str = character.string + str;
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder clear() {
        str = &quot;&quot;;
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder insert(Integer index, String string) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        str = str[0:index] + string + str[index...];
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder insertCharacter
            (Integer index, Character character) 
            =&gt; insert(index, character.string);
    
    shared native(&quot;js&quot;) 
    StringBuilder replace
            (Integer index, Integer length, String string) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        &quot;index+length must not be greater than size&quot;
        assert (index+length&lt;=size);
        if (!string.empty) {
            str = str[0:index] + string + str[index+length...];
        }
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder delete(Integer index, Integer length) {
        &quot;index must not be negative&quot;
        assert (index&gt;=0);
        &quot;index must not be greater than size&quot;
        assert(index&lt;=size);
        &quot;index+length must not be greater than size&quot;
        assert (index+length&lt;=size);
        if (length&gt;0) {
            str = str[0:index] + str[index+length...];
        }
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder deleteInitial(Integer length) {
        &quot;length must not be greater than size&quot;
        assert (length&lt;=size);
        if (length&gt;0) {
            str = str[length...];
        }
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder deleteTerminal(Integer length) {
        &quot;length must not be greater than size&quot;
        assert (length&lt;=size);
        if (length&gt;0) {
            str = str[0:size-length];
        }
        return this;
    }
    
    shared native(&quot;js&quot;) 
    StringBuilder reverseInPlace() {
        str = str.reversed;
        return this;
    }
    
    shared actual native(&quot;js&quot;) Boolean equals(Object that) 
            =&gt; str.equals(that);
    
    shared actual native(&quot;js&quot;) Integer hash =&gt; str.hash;
}
</pre></body></html>