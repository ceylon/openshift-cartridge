<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Singleton.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;A [[Sequence]] with exactly one [[element]], which may be 
 null.&quot;
tagged(&quot;Sequences&quot;)
shared final serializable class Singleton&lt;out Element&gt;
        (Element element)
        extends Object()
        satisfies [Element+] {
    
    &quot;Returns the element contained in this `Singleton`.&quot;
    shared actual Element first =&gt; element;
    
    &quot;Returns the element contained in this `Singleton`.&quot;
    shared actual Element last =&gt; element;
    
    &quot;Returns `0`.&quot;
    shared actual Integer lastIndex =&gt; 0;
    
    &quot;Returns `1`.&quot;
    shared actual Integer size =&gt; 1;
    
    &quot;Returns `Empty`.&quot;
    shared actual Empty rest =&gt; [];
    
    &quot;Returns the contained element, if the specified 
     index is `0`.&quot;
    shared actual Element? getFromFirst(Integer index)
            =&gt; if (index == 0) then element else null;
    
    &quot;Return this singleton.&quot;
    shared actual Singleton&lt;Element&gt; reversed =&gt; this;
    
    &quot;Returns a `Singleton` with the same element.&quot;
    shared actual 
    Singleton&lt;Element&gt; clone() =&gt; this;
    
    &quot;Returns `true` if the specified element is this 
     `Singleton`\&apos;s element.&quot;
    shared actual Boolean contains(Object element)
            =&gt; if (exists e = this.element) 
            then e == element
            else false;
    
    string =&gt; &quot;[``stringify(element)``]&quot;;
    
    shared actual Iterator&lt;Element&gt; iterator()
            =&gt; object 
            satisfies Iterator&lt;Element&gt; {
        variable Boolean done = false;
        shared actual Element|Finished next() {
            if (done) {
                return finished;
            }
            else {
                done = true;
                return element;
            }
        }
        string =&gt; &quot;``outer.string``.iterator()&quot;;
    };
    
    &quot;A `Singleton` can be equal to another `List` if 
     that `List` has only one element which is equal to 
     this `Singleton`\&apos;s element.&quot;
    shared actual Boolean equals(Object that) {
        if (is List&lt;Anything&gt; that, that.size == 1) {
            value elem = that.first;
            return if (exists element, exists elem) 
            then elem == element
            else !element exists &amp;&amp; !elem exists;
        }
        else {
            return false;
        }
    }
    
    shared actual Integer hash 
            =&gt; 31 + (element?.hash else 0);
    
    &quot;Returns a `Singleton` if the given starting index 
     is `0` and the given `length` is greater than `0`.
     Otherwise, returns an instance of `Empty`.&quot;
    shared actual 
    Empty|Singleton&lt;Element&gt; measure
            (Integer from, Integer length)
            =&gt; from &lt;= 0 &amp;&amp; from + length &gt; 0 
            then this else [];
    
    &quot;Returns a `Singleton` if the given starting index 
     is `0`. Otherwise, returns an instance of `Empty`.&quot;
    shared actual 
    Empty|Singleton&lt;Element&gt; span
            (Integer from, Integer to)
            =&gt; from &lt;= 0 &amp;&amp; to &gt;= 0 ||
               from &gt;= 0 &amp;&amp; to &lt;= 0
            then this else [];
    
    shared actual 
    Empty|Singleton&lt;Element&gt; spanTo(Integer to) 
            =&gt; to &lt; 0 then [] else this;
    
    shared actual 
    Empty|Singleton&lt;Element&gt; spanFrom(Integer from) 
            =&gt; from &gt; 0 then [] else this;
    
    &quot;Returns `1` if this `Singleton`\&apos;s element
     satisfies the predicate, or `0` otherwise.&quot;
    shared actual 
    Integer count(Boolean selecting(Element element))
            =&gt; selecting(element) then 1 else 0;
    
    shared actual 
    Singleton&lt;Result&gt; map&lt;Result&gt;
            (Result collecting(Element e))
            =&gt; Singleton(collecting(element));
    
    shared actual 
    Singleton&lt;Element&gt;|[] filter
            (Boolean selecting(Element e))
            =&gt; selecting(element) then this else [];
    
    shared actual 
    Result fold&lt;Result&gt;(Result initial)
            (Result accumulating(Result partial, 
                                 Element e))
            =&gt; accumulating(initial, element);
    
    shared actual 
    Element reduce&lt;Result&gt;
            (Result accumulating(Result|Element partial, 
                                 Element e))
            =&gt; element;
    
    shared actual 
    Singleton&lt;Result&gt; collect&lt;Result&gt;
            (Result collecting(Element element))
            =&gt; Singleton(collecting(element));
    
    shared actual 
    Singleton&lt;Element&gt;|[] select
            (Boolean selecting(Element element))
            =&gt; selecting(element) then this else [];
    
    shared actual 
    Element? find(Boolean selecting(Element&amp;Object e))
            =&gt; if (exists element, selecting(element))
                    then element else null;
    
    shared actual 
    Element? findLast(Boolean selecting(Element&amp;Object e))
            =&gt; find(selecting);
    
    shared actual 
    Singleton&lt;Element&gt; sort
            (Comparison comparing(Element a, Element b))
            =&gt; this;
    
    shared actual 
    Boolean any(Boolean selecting(Element e))
            =&gt; selecting(element);
    
    shared actual 
    Boolean every(Boolean selecting(Element e))
            =&gt; selecting(element);
    
    shared actual 
    Singleton&lt;Element&gt;|Empty skip(Integer skipping)
            =&gt; skipping &lt; 1 then this else [];
    
    shared actual 
    Singleton&lt;Element&gt;|Empty take(Integer taking)
            =&gt; taking &gt; 0 then this else [];
    
    shared actual 
    Singleton&lt;Element&amp;Object&gt;|Empty coalesced
            =&gt; if (exists element)
                    then Singleton(element) else [];
    
    shared actual 
    {Element|Other+} chain&lt;Other,OtherAbsent&gt;
            (Iterable&lt;Other,OtherAbsent&gt; other)
            given OtherAbsent satisfies Null
            =&gt; other.follow(element);
    
    each(void step(Element element)) =&gt; step(element);
    
    indexed =&gt; { 0-&gt;element };
    
    &quot;A stream with given [[head]], followed by the [[element]]
     of this singleton.
     
     For example, the expression
     
         Singleton(1).follow(2)
     
     evaluates to the stream `{ 2, 1 }`.&quot;
    shared actual {Other|Element+} follow&lt;Other&gt;(Other head) 
            =&gt; { head, element };
    
    &quot;An infinite stream that produces the [[element]] of 
     this singleton, repeatedly.
     
     For example, the expression
     
         Singleton(null).cycled.take(4)
     
     evaluates to the stream `{ null, null, null, null }`.&quot;
    see (`function repeat`)
    shared actual 
    {Element+} cycled 
            =&gt; object satisfies {Element+} {
        string =&gt; outer.string + &quot;.cycled&quot;;
        shared actual Integer size {
            &quot;stream is infinite&quot; 
            assert (false); 
        }
        iterator() 
                =&gt; object satisfies Iterator&lt;Element&gt; {
            next() =&gt; element; 
            string =&gt; outer.string + &quot;.iterator()&quot;;
        };
    };
    
    &quot;This singleton.&quot;
    shared actual
    Singleton&lt;Element&gt; interpose&lt;Other&gt;(
        Other element,
        Integer step) {
        &quot;step must be strictly positive&quot;
        assert (step&gt;=1);
        return this;
    }
    
    &quot;This singleton.&quot;
    shared actual Singleton&lt;Element&gt; distinct =&gt; this;
    
    &quot;An empty stream.&quot;
    shared actual [] paired =&gt; [];
    
}
</pre></body></html>