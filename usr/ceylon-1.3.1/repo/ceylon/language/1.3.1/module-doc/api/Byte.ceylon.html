<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Byte.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;An 8-bit byte. A `Byte` value represents a congruence class
 of [[integers|Integer]] modulo 256, and may be interpreted 
 as:
 
 - an [[unsigned]] integer value in the range `0..255`, or 
 - a [[signed]] integer value in the range `-128..127`. 
 
 `Byte` is not considered a full numeric type, supporting 
 only:
 
 - [[bitwise|Binary]] operations, and 
 - addition and subtraction modulo 256.
 
 `Byte`s with modular addition form a [[mathematical 
 group|Invertible]]. Thus, every byte `b` has an additive
 inverse `-b` where:
 
     (-b).signed == -b.signed
     (-b).unsigned == b.unsigned==0 then 0 else 256 - b.unsigned
 
 `Byte` is a [[recursive enumerable type|Enumerable]]. For
 example, the range:
 
     254.byte .. 1.byte
     
 contains the values `254.byte, 255.byte, 0.byte, 1.byte`.
 
 `Byte` does not have a [[total order|Comparable]] because
 any such order would:
  
 - be inconsistent with the definition of [[successor]] and 
   [[predecessor]] under modular addition, and
 - would depend on interpretation of the `Byte` value as
   signed or unsigned.
 
 Thus, to compare the magnitude of two bytes, it is 
 necessary to first convert them to either their `signed` or 
 `unsigned` integer values.
 
 `Byte`s are useful mainly because they can be efficiently 
 stored in an [[Array]].&quot;
tagged(&quot;Basic types&quot;)
since(&quot;1.1.0&quot;)
shared native final class Byte(congruent) 
        extends Object()
        satisfies Binary&lt;Byte&gt; &amp; 
                  Invertible&lt;Byte&gt; &amp;
                  Enumerable&lt;Byte&gt; {
    
    &quot;An integer member of the congruence class of the 
     resulting `Byte`.
     
     For any integer `x&gt;=0`:
     
         x.byte.unsigned == x % 256
         x.byte.signed == x % 256
     
     And for an integer `x&lt;0`:
     
         x.byte.unsigned == 256 + x % 256
         x.byte.signed == x % 256
     
     And for any integers `x` and `y` which are congruent
     modulo 256:
     
         x.byte == y.byte&quot;
    Integer congruent;
    
    &quot;This byte interpreted as an unsigned integer in the
     range `0..255` (that is, `0..#FF`).&quot;
    shared native Integer unsigned 
            = (congruent % #100).and(#FF);
    
    &quot;This byte interpreted as a signed integer in the range 
     `-128..127` (that is, `-#80..#7F`).&quot;
    shared native Integer signed 
            =&gt; unsigned &gt; #7F 
            then unsigned - #100 
            else unsigned;
    
    //shared Boolean[8] bits;
    
    &quot;Whether this byte is even.&quot;
    shared native Boolean even =&gt; and(1.byte).zero;
    
    &quot;Whether this byte is zero.&quot;
    shared native Boolean zero =&gt; unsigned == 0;
    
    &quot;Whether this byte is one.&quot;
    shared native Boolean unit =&gt; unsigned == 1;
    
    &quot;The additive inverse of this byte. For any integer `x`:
     
         (-x.byte).signed = -x.byte.signed&quot;
    shared actual native Byte negated =&gt; (-signed).byte;
    
    &quot;The modulo 256 sum of this byte and the given byte.&quot;
    shared actual native Byte plus(Byte other)
            =&gt; (this.unsigned + other.unsigned).byte;
    
    function indexInRange(Integer index)
            =&gt; 0 &lt;= index &lt; 8;
    
    function mask(Integer index) 
            =&gt; 1.byte.leftLogicalShift(index);
    
    shared actual native Boolean get(Integer index) 
            =&gt; indexInRange(index)
            then !and(mask(index)).zero 
            else false;
    
    shared actual native Byte flip(Integer index)
            =&gt; indexInRange(index)
            then xor(mask(index)) 
            else this;
    
    shared actual native Byte set(Integer index, Boolean bit)
            =&gt; indexInRange(index)
            then (bit then or(mask(index)) 
                      else and(mask(index).not))
            else this;
    
    shared actual native Byte clear(Integer index) 
            =&gt; indexInRange(index)
            then and(mask(index).not) 
            else this;
    
    shared actual native Byte not =&gt; unsigned.not.byte;
    
    shared actual native Byte and(Byte other) 
            =&gt; unsigned.and(other.unsigned).byte;
    
    shared actual native Byte or(Byte other)
            =&gt; unsigned.or(other.unsigned).byte;
    
    shared actual native Byte xor(Byte other)
            =&gt; unsigned.xor(other.unsigned).byte;
    
    &quot;If [[shift]] is in the range `0..$111`, shift the bits 
     to the left by `shift` positions, using zero extension 
     to fill in the least significant bits. Otherwise shift 
     the addressable bits to the left by `shift.and($111)`
     positions, using zero extension.&quot;
    aliased (&quot;leftShift&quot;)
    shared actual native Byte leftLogicalShift(Integer shift)
            =&gt; unsigned.leftLogicalShift(shift.and($111)).byte;
    
    &quot;If [[shift]] is in the range `0..$111`, shift the bits 
     to the right by `shift` positions, using zero extension 
     to fill in the most significant bits. Otherwise shift 
     the addressable bits to the right by `shift.and($111)`
     positions, using zero extension.&quot;
    aliased (&quot;rightShift&quot;)
    shared actual native Byte rightLogicalShift(Integer shift)
            =&gt; unsigned.rightLogicalShift(shift.and($111)).byte;
    
    &quot;If [[shift]] is in the range `0..$111`, shift the bits 
     to the right by `shift` positions, using sign extension 
     to fill in the most significant bits. Otherwise shift 
     the addressable bits to the right by `shift.and($111)`
     positions, using sign extension.&quot;
    shared actual native Byte rightArithmeticShift(Integer shift)
            =&gt; signed.rightArithmeticShift(shift.and($111)).byte;
    
    shared actual native Byte predecessor =&gt; (unsigned-1).byte;
    shared actual native Byte successor =&gt; (unsigned+1).byte;
    
    shared actual native Byte neighbour(Integer offset) 
            =&gt; (unsigned + offset).byte;
    
    shared actual native Integer offset(Byte other)
            =&gt; minus(other).unsigned;
    
    shared actual native Integer offsetSign(Byte other)
            =&gt; this==other then 0 else 1;
    
    shared actual native Boolean equals(Object that) 
            =&gt; if (is Byte that) 
            then this.unsigned == that.unsigned
            else false;
    
    shared actual native Integer hash =&gt; signed;
    
    &quot;The [[unsigned]] interpretation of this byte as a 
     string.&quot;
    shared actual native String string =&gt; unsigned.string;
    
}
</pre></body></html>