<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>sum.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;Given a nonempty stream of [[Summable]] values, return the 
 sum of the values.
 
     {Float+} values = ... ;
     Float total = sum(values);
 
 For the case of a possibly-empty stream, form a nonempty 
 stream starting with the zero element (the [[additive 
 identity|Summable]]).
 
     {Float*} values = ... ;
     Float total = sum { 0.0, *values };&quot;
see (`function product`)
tagged(&quot;Streams&quot;, &quot;Numbers&quot;)
shared native Value sum&lt;Value&gt;({Value+} values) 
        given Value satisfies Summable&lt;Value&gt;;

shared native(&quot;js&quot;) Value sum&lt;Value&gt;({Value+} values) 
        given Value satisfies Summable&lt;Value&gt; {
    value it = values.iterator();
    assert (!is Finished first = it.next());
    variable value sum = first;
    while (!is Finished val = it.next()) {
        sum += val;
    }
    return sum;
}

shared native(&quot;jvm&quot;) Value sum&lt;Value&gt;({Value+} values) 
        given Value satisfies Summable&lt;Value&gt; {
    value it = values.iterator();
    switch (first = it.next())
    case (is Integer) {
        // unbox; don&apos;t infer type Value&amp;Integer
        variable Integer sum = first;
        while (is Integer val = it.next()) {
            Integer unboxed = val;
            sum += unboxed;
        }
        assert (is Value result = sum);
        return result;
    }
    case (is Float) {
        // unbox; don&apos;t infer type Value&amp;Float
        variable Float sum = first;
        while (is Float val = it.next()) {
            Float unboxed = val;
            sum += unboxed;
        }
        assert (is Value result = sum);
        return result;
    }
    case (is Finished) {
        assert (false);
    }
    else {
        variable value sum = first;
        while (!is Finished val = it.next()) {
            sum += val;
        }
        return sum;
    }
}
</pre></body></html>