<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Measure.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;A [[Range]] of adjacent [[Enumerable]] values generated by 
 a [[first]] element, and a strictly positive [[size]]. The 
 range includes all values whose offset from `first` is 
 non-negative and less than the `size`.&quot;
see (`class Span`,
    `interface Enumerable`)
final serializable
class Measure&lt;Element&gt;(first, size)
        extends Range&lt;Element&gt;()
        given Element satisfies Enumerable&lt;Element&gt; {
    
    &quot;The start of the range.&quot;
    shared actual Element first;
    
    &quot;The size of the range.&quot;
    shared actual Integer size;
    
    &quot;must be nonempty&quot;
    assert (size &gt; 0);
    
    string =&gt; first.string + &quot;:&quot; + size.string;
    
    last =&gt; first.neighbour(size - 1);
    
    longerThan(Integer length) =&gt; size &gt; length;
    
    shorterThan(Integer length) =&gt; size &lt; length;
    
    lastIndex =&gt; size - 1;
    
    rest =&gt; size &gt; 1 
            then Measure(first.successor, size - 1)
            else [];
    
    getFromFirst(Integer index) 
            =&gt; index &gt;= 0 &amp;&amp; index &lt; size
            then first.neighbour(index)
            else null;
    
    increasing =&gt; true;
    decreasing =&gt; false;
    
    iterator()
            =&gt; object satisfies Iterator&lt;Element&gt; {
        variable value count = 0;
        variable value current = first;
        shared actual Element|Finished next() {
            if (count &gt;= size) {
                return finished;
            }
            else if (count++ == 0) {
                return current;
            }
            else {
                return ++current;
            }
        }
        string =&gt; &quot;(``outer.string``).iterator()&quot;;
    };
    
    shared actual 
    {Element+} by(Integer step) {
        &quot;step size must be greater than zero&quot;
        assert (step &gt; 0);
        return step == 1 then this else By(step);
    }
    
    class By(Integer step)
            satisfies {Element+} {
        
        size =&gt; 1 + (outer.size - 1) / step;
        
        first =&gt; outer.first;
        
        string =&gt; &quot;(``outer``).by(``step``)&quot;;
        
        iterator() =&gt; object
                satisfies Iterator&lt;Element&gt; {
            variable value count = 0;
            variable value current = first;
            shared actual Element|Finished next() {
                if (count &gt;= size) {
                    return finished;
                }
                else if (count++ == 0) {
                    return current;
                }
                else {
                    current = current.neighbour(step);
                    return current;
                }
            }
            string =&gt; &quot;``outer``.iterator()&quot;;
        };
    }
    
    shifted(Integer shift) 
            =&gt; shift == 0 
            then this 
            else Measure(first.neighbour(shift), size);
    
    containsElement(Element x)
            =&gt; 0 &lt;= x.offset(first) &lt; size;
    
    shared actual 
    Boolean includesRange(Range&lt;Element&gt; range) {
        switch (range)
        case (is Measure&lt;Element&gt;) {
            value offset = range.first.offset(first);
            return 0 &lt;= offset &lt;= size - range.size;
        }
        case (is Span&lt;Element&gt;) {
            if (range.decreasing) {
                return false;
            } else {
                value offset = range.first.offset(first);
                return 0 &lt;= offset &lt;= size - range.size;
            }
        }
    }
    
    shared actual 
    Boolean equals(Object that) {
        if (is Measure&lt;out Object&gt; that) {
            //optimize for another Measure
            return that.size == size &amp;&amp; that.first == first;
        } else if (is Span&lt;out Object&gt; that) {
            return that.increasing &amp;&amp;
                    that.first == first &amp;&amp; that.size == size;
        } else {
            //it might be another sort of List
            return super.equals(that);
        }
    }
    
    shared actual 
    Element[] measure(Integer from, Integer length) {
        if (length &lt;= 0) {
            return [];
        } else {
            value len = from + length &lt; size 
                    then length
                    else size - from;
            return Measure(first.neighbour(from), len);
        }
    }
    
    shared actual 
    Element[] span(Integer from, Integer to) {
        if (from &lt;= to) {
            if (to &lt; 0 || from &gt;= size) {
                return [];
            } else {
                value len = to &lt; size 
                        then to - from + 1
                        else size - from;
                return Measure(first.neighbour(from), len);
            }
        } else {
            if (from &lt; 0 || to &gt;= size) {
                return [];
            } else {
                value len = from &lt; size 
                        then from - to + 1
                        else size - to;
                return Measure(first.neighbour(to), len).reversed;
            }
        }
    }
    
    shared actual 
    Element[] spanFrom(Integer from) {
        if (from &lt;= 0) {
            return this;
        } else if (from &lt; size) {
            return Measure(first.neighbour(from), size - from);
        } else {
            return [];
        }
    }
    
    shared actual 
    Element[] spanTo(Integer to) {
        if (to &lt; 0) {
            return [];
        } else if (to &lt; size - 1) {
            return Measure(first, to);
        } else {
            return this;
        }
    }
    
    shared actual void each(void step(Element element)) {
        variable value current = first;
        variable value count = 0;
        while (count++&lt;size) {
            step(current++);
        }
    }
}

&quot;Produces a [[Range]] of adjacent [[Enumerable]] values 
 generated by a [[first]] element, and a strictly positive 
 [[size]], or returns the [[empty sequence|empty]] if 
 `size &lt;= 0`. The range includes all values whose offset 
 from `first` is non-negative and less than the `size`.
 
 More precisely, if `x` and `first` are of `Enumerable` 
 type `X`, and `size` is an integer, then `x in first:size` 
 if and only if `0 &lt;= x.offset(first) &lt; size`.
 
 The _measure operator_ `:` is an abbreviation for
 `measure()`:
 
     for (i in start:size) { ... }
     for (char in &apos;0&apos;:10) { ... }
 
 The measure operator accepts the first index and size of 
 the range:
 
     0:5     // [0, 1, 2, 3, 4]
 
 If the size is nonpositive, the range is empty:
 
     0:0     // []
     5:0     // []
     0:-5    // []&quot;
since(&quot;1.1.0&quot;)
shared Range&lt;Element&gt;|[] measure&lt;Element&gt;
        (Element first, Integer size)
        given Element satisfies Enumerable&lt;Element&gt;
        =&gt; size &lt;= 0 then [] else Measure(first, size);
</pre></body></html>