<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>product.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;Given a nonempty stream of [[Numeric]] values, return the 
 product of the values.
 
     {Float+} values = ... ;
     Float result = product(values);
 
 For the case of a possibly-empty stream, form a nonempty 
 stream starting with the unit element (the [[multiplicative
 identity|Numeric]]).
 
     {Float*} values = ... ;
     Float result = product { 1.0, *values };&quot;
see (`function sum`)
tagged(&quot;Streams&quot;, &quot;Numbers&quot;)
shared native Value product&lt;Value&gt;({Value+} values) 
        given Value satisfies Numeric&lt;Value&gt;;

shared native(&quot;js&quot;) Value product&lt;Value&gt;({Value+} values) 
        given Value satisfies Numeric&lt;Value&gt; {
    value it = values.iterator();
    assert (!is Finished first = it.next());
    variable value product = first;
    while (!is Finished val = it.next()) {
        product *= val;
    }
    return product;
}

shared native(&quot;jvm&quot;) Value product&lt;Value&gt;({Value+} values) 
        given Value satisfies Numeric&lt;Value&gt; {
    value it = values.iterator();
    switch (first = it.next())
    case (is Integer) {
        // unbox; don&apos;t infer type Value&amp;Integer
        variable Integer product = first;
        while (is Integer val = it.next()) {
            Integer unboxed = val;
            product *= unboxed;
        }
        assert (is Value result = product);
        return result;
    }
    case (is Float) {
        // unbox; don&apos;t infer type Value&amp;Float
        variable Float product = first;
        while (is Float val = it.next()) {
            Float unboxed = val;
            product *= unboxed;
        }
        assert (is Value result = product);
        return result;
    }
    case (is Finished) {
        assert (false);
    }
    else {
        variable value product = first;
        while (!is Finished val = it.next()) {
            product *= val;
        }
        return product;
    }
}
</pre></body></html>