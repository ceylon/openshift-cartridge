<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>List.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;&quot;&quot;A collection in which every element has a unique 
   non-negative integer index. The elements of a nonempty 
   list are indexed starting with `0` at the [[first]] 
   element of the list, and ending with the index 
   [[lastIndex]] at the [[last]] element of the list.
   
   - For any nonempty list, `lastIndex==size-1`. 
   - For an empty list, `size==0` and the `lastIndex` is 
     `null`.
   
   Thus, the range of indexes of the list is formed by the 
   expression `0:list.size`.
   
   A `List` is a [[Collection]] of its elements, and a 
   [[Correspondence]] from indexes to elements.
   
   Every list has a well-defined and stable iteration order.
   An [[iterator]] of a nonempty list is required to return 
   the elements of the list in order of increasing index, 
   beginning with the element at index `0`, and ending with
   the element at index `lastIndex`. Thus, every iterator of 
   an immutable list produces exactly the same elements in 
   exactly the same order.
   
   Direct access to a list element by index produces a value 
   of optional type. The following idiom may be used instead 
   of upfront bounds-checking, as long as the list element 
   type is a non-`null` type:
   
       if (exists char = &quot;hello world&quot;[index]) { 
           //do something with char
       }
       else {
           //out of bounds
       }
   
   When an algorithm guarantees that a list contains a given 
   index, the following idiom may be used:
   
       assert (exists char = &quot;hello world&quot;[index]);
       //do something with char
   
   To iterate the indexes of a `List`, use the following
   idiom:
   
       for (i-&gt;char in &quot;hello world&quot;.indexed) { ... }
   
   [[Strings|String]], [[sequences|Sequential]], 
   [[tuples|Tuple]], and [[arrays|Array]] are all `List`s,
   and are all of fixed length. Variable-length mutable
   `List`s are also possible.&quot;&quot;&quot;
see (`interface Sequence`, 
     `interface Empty`, 
     `class Array`)
tagged(&quot;Collections&quot;)
shared interface List&lt;out Element=Anything&gt;
        satisfies Collection&lt;Element&gt; &amp;
                  Correspondence&lt;Integer,Element&gt; &amp;
                  Ranged&lt;Integer,Element,List&lt;Element&gt;&gt; {
    
    &quot;The first element of this `List`, if any.&quot;
    shared actual default Element? first =&gt; getFromFirst(0);
    
    &quot;The last element of this `List`, if any.&quot;
    shared actual default Element? last =&gt; getFromLast(0);
    
    &quot;Returns the element of this list with the given 
     [[index]] if the index refers to an element of this
     list, that is, if `0&lt;=index&lt;=list.lastIndex`, or `null` 
     otherwise. The first element of the list has index `0`,
     and the last element has index [[lastIndex]].&quot;
    shared actual Element? get(Integer index) 
            =&gt; getFromFirst(index);
    
    &quot;Returns the element of this list with the given 
     [[index]] if the index refers to an element of this
     list, that is, if `0&lt;=index&lt;=list.lastIndex`, or `null` 
     otherwise. The first element of the list has index `0`, 
     and the last element has index [[lastIndex]].&quot;
    see (`function getFromLast`)
    shared actual formal Element? getFromFirst(Integer index);
    
    &quot;Returns the element of this list with the given 
     [[index]], where the list is indexed from the _end_ of 
     the list instead of from the start, if the index refers
     to an element of this list, or `null` otherwise. The
     last element of the list has index `0`, and the first
     element has index [[lastIndex]].&quot;
    since(&quot;1.1.0&quot;)
    shared default Element? getFromLast(Integer index)
            =&gt; getFromFirst(size-1-index);
    
    Element getElement(Integer index) {
        if (exists element = getFromFirst(index)) { 
            return element;
        }
        else {
            assert (is Element null);
            return null; 
        }
    }
    
    &quot;The index of the last element of the list, or `null` if 
     the list is empty. Always `size&gt;0 then size-1`.&quot;
    see (`value List.size`)
    shared formal Integer? lastIndex;
    
    &quot;The number of elements in this list, always
     `1 + (lastIndex else -1)`.&quot;
    see (`value List.lastIndex`)
    shared actual default Integer size 
            =&gt; 1 + (lastIndex else -1);
    
    &quot;Determines if the given index refers to an element of 
     this list, that is, if `0&lt;=index&lt;=list.lastIndex`.&quot;
    shared actual default Boolean defines(Integer index) 
            =&gt; 0 &lt;= index &lt; size;
    
    &quot;Determines if this list contains the given value.
     Returns `true` for every element of this list.&quot;
    shared actual default Boolean contains(Object element) {
        for (index in 0:size) {
            if (exists elem = getFromFirst(index), 
                    elem==element) {
                return true;
            }
        }
        else {
            return false;
        }
    }
    
    &quot;The rest of the list, without the first element.
     
     This is a lazy operation returning a view of this list.&quot;
    shared actual default List&lt;Element&gt; rest =&gt; Rest(1);
    
    &quot;A list containing all indexes of this list.
     
     This is a lazy operation returning a view of this list.&quot;
    see (`function indexes`)
    shared actual default List&lt;Integer&gt; keys =&gt; Indexes();
    
    &quot;A list containing the elements of this list in reverse 
     order to the order in which they occur in this list. 
     For every `index` of a reversed `list`:
     
         list.reversed[index]==list[size-1-index]
     
     This is a lazy operation returning a view of this list.&quot;
    shared default List&lt;Element&gt; reversed =&gt; Reversed();
    
    &quot;A shallow copy of this list, that is, a list with the
     same elements as this list, which do not change if the
     elements of this list change.&quot;
    shared actual default List&lt;Element&gt; clone() 
            =&gt; sequence();
    
    shared actual default Iterator&lt;Element&gt; iterator() {
        if (size&gt;0) {
            return object
                    satisfies Iterator&lt;Element&gt; {
                variable Integer index = 0;
                value size = outer.size;
                next() =&gt; index&gt;=size
                    then finished
                    else getElement(index++);
                string =&gt; outer.string + &quot;.iterator()&quot;;
            };
        }
        else {
            return emptyIterator;
        }
    }
    
    &quot;Two `List`s are considered equal iff they have the 
     same `size` and _entry sets_. The entry set of a list 
     `list` is the set of elements of `list.indexed`. This 
     definition is equivalent to the more intuitive notion 
     that two lists are equal iff they have the same `size` 
     and for every index either:
     
     - the lists both have the element `null`, or
     - the lists both have a non-null element, and the
       two elements are equal.
     
     As a special exception, a [[String]] is not equal to 
     any list which is not also a [[String]].&quot;
    shared actual default Boolean equals(Object that) {
        if (is String that) {
            return false;
        }
        if (is List&lt;&gt; that) {
            if (that.size==size) {
                for (index in 0:size) {
                    value x = getFromFirst(index);
                    value y = that.getFromFirst(index);
                    if (exists x) {
                        if (exists y) {
                            if (x!=y) {
                                return false;
                            }
                        }
                        else {
                            return false;
                        }
                    }
                    else if (exists y) {
                        return false;
                    }
                }
                else {
                    return true;
                }
            }
            else {
                return false;
            }
        }
        else {
            return false;
        }
    }
    
    shared actual default Integer hash {
        variable value hash = 1;
        for (elem in this) {
            hash *= 31;
            if (exists elem) {
                hash += elem.hash;
            }
        }
        return hash;
    }
    
    shared actual default
    Boolean shorterThan(Integer length) =&gt; size&lt;length;
    
    shared actual default 
    Boolean longerThan(Integer length) =&gt; size&gt;length;
    
    &quot;A list containing the elements of this list repeated 
     the [[given number of times|times]], or an empty list
     if `times&lt;=0`. For every `index` of a repeated `list`:
     
         list.repeat(n)[index]==list[index%n]
     
     This is a lazy operation returning a view of this list.&quot;
    shared actual default 
    List&lt;Element&gt; repeat(Integer times) =&gt; Repeat(times);
    
    shared default actual 
    Element? find(
            Boolean selecting(Element&amp;Object elem)) {
        variable value index = 0;
        while (index&lt;size) {
            if (exists elem = getFromFirst(index++)) {
                if (selecting(elem)) {
                    return elem;
                }
            }
        }
        return null;
    }
    
    shared default actual 
    Element? findLast(
            Boolean selecting(Element&amp;Object elem)) {
        variable value index = size-1;
        while (index &gt;= 0) {
            if (exists elem = getFromFirst(index--), 
                    selecting(elem)) {
                return elem;
            }
        }
        return null;
    }
    
    &quot;A sublist of this list, starting at the element with
     the given [[index|from]].
     
     This is a lazy operation, returning a view of this list.&quot;
    see (`function skip`)
    since(&quot;1.1.0&quot;)
    shared default 
    List&lt;Element&gt; sublistFrom(Integer from) 
            =&gt; from&lt;=0 then this else Rest(from); 
    
    &quot;A sublist of this list, ending at the element with the 
     given [[index|to]].
     
     This is a lazy operation, returning a view of this list.&quot;
    see (`function take`,
        `function initial`)
    since(&quot;1.1.0&quot;)
    shared default 
    List&lt;Element&gt; sublistTo(Integer to) 
            =&gt; to&lt;0 then [] else Sublist(to);
    
    &quot;A sublist of this list, starting at the element with
     index [[from]], ending at the element with the index 
     [[to]].
     
     This is a lazy operation, returning a view of this list.&quot;
    since(&quot;1.1.0&quot;)
    shared default 
    List&lt;Element&gt; sublist(Integer from, Integer to) 
            =&gt; sublistTo(to).sublistFrom(from);
    
    &quot;Return a list formed by patching the given [[list]] 
     in place of a segment of this list identified by the
     given [[starting index|from]] and [[length]].
     
     This is a lazy operations, returning a view over this 
     list and the given list.
     
     Four special cases are interesting:
     
     - If `length==0`, the patched list has the given values 
       \&quot;inserted\&quot; into this list at the given index `from`.
     - If the given `list` is empty, the patched list has 
       the measure of this list identified by `from:length` 
       \&quot;deleted\&quot;.
     - If `from==size`, the patched list is formed by
       appending the given list.
     - If `from==0`, the patched list is formed by 
       prepending the given list.
     
     For example:
     
     - `(-2..2).patch([], 1, 3)` produces the list `{-2, 2}`,
     - `[-2, 2].patch(-1..1, 1)` produces the list 
       `{-2, -1, 0, 1, 2}`, and
     - `0:3.patch(2..0)` produces the list 
       `{0, 1, 2, 2, 1, 0}`.
     
     Finally, to patch a single element, leaving the `size`
     of the list unchanged, explicitly specify `length==1`:
     
     - `[0, 1, 0, 1].patch([-1], 2, 1)` produces the list
       `{0, 1, -1, 1}`.
     
     If `length&lt;0`, or if `from` is outside the range 
     `0..size`, return this list.&quot;
    since(&quot;1.1.0&quot;)
    shared default 
    List&lt;Element|Other&gt; patch&lt;Other&gt;(
        &quot;The list of new elements.&quot;
        List&lt;Other&gt; list,
        &quot;The index at which the elements will occur, and
         the start index of the segment to replace.&quot;
        Integer from=size,
        &quot;The length of the segment to replace.&quot; 
        Integer length=0)
            =&gt; length&gt;=0 &amp;&amp; 0&lt;=from&lt;=size
            then Patch(list, from, length)
            else this;
    
    &quot;Determine if the given [[list|sublist]] occurs at the 
     start of this list.&quot;
    see (`function endsWith`)
    shared default 
    Boolean startsWith(List&lt;&gt; sublist) 
            =&gt; !shorterThan(sublist.size) 
            &amp;&amp; everyPair&lt;Element,Anything&gt;(
                (first, second)
                    =&gt; if (exists first, exists second)
                        then first==second
                        else first exists == second exists, 
                this, 
                sublist);
    
    &quot;Determine if the given [[list|sublist]] occurs at the 
     end of this list.&quot;
    see (`function startsWith`)
    shared default 
    Boolean endsWith(List&lt;&gt; sublist)
            =&gt; !shorterThan(sublist.size) 
            &amp;&amp; everyPair&lt;Element,Anything&gt;(
                (first, second)
                    =&gt; if (exists first, exists second)
                        then first==second
                        else first exists == second exists, 
                skip(size-sublist.size), 
                sublist);
    
    &quot;The indexes in this list for which the element is not
     null and satisfies the given 
     [[predicate function|selecting]].&quot;
    see (`function locations`)
    since(&quot;1.1.0&quot;)
    shared default 
    {Integer*} indexesWhere(
        &quot;The predicate function the indexed elements must 
         satisfy.&quot;
        Boolean selecting(Element&amp;Object element)) 
            =&gt; { for (index in 0:size) 
                    if (exists element=getFromFirst(index), 
                            selecting(element)) 
                        index };
    
    &quot;The first index in this list for which the element is
     not null and satisfies the given 
     [[predicate function|selecting]].&quot;
    see (`function locate`)
    since(&quot;1.1.0&quot;)
    shared default 
    Integer? firstIndexWhere(
        &quot;The predicate function the indexed elements must 
         satisfy.&quot;
        Boolean selecting(Element&amp;Object element)) {
        variable value index = 0;
        while (index&lt;size) {
            if (exists element=getFromFirst(index), 
                selecting(element)) {
                return index;
            }
            index++;
        }
        return null;
    }
    
    &quot;The last index in this list for which the element is
     not null and satisfies the given 
     [[predicate function|selecting]].&quot;
    see (`function locateLast`)
    since(&quot;1.1.0&quot;)
    shared default 
    Integer? lastIndexWhere(
        &quot;The predicate function the indexed elements must 
         satisfy.&quot;
        Boolean selecting(Element&amp;Object element)) {
        variable value index = size;
        while (index&gt;0) {
            index--;
            if (exists element=getFromFirst(index), 
                    selecting(element)) {
                return index;
            }
        }
        return null;
    }
    
    &quot;Trim the elements satisfying the given [[predicate 
     function|trimming]], along with any null elements, from 
     the start and end of this list, returning a list no 
     longer than this list.
     
     This is an eager operation.&quot;
    shared default 
    List&lt;Element&gt; trim(
        &quot;The predicate function that the trimmed elements 
         satisfy.&quot;
        Boolean trimming(Element&amp;Object elem)) {
        if (size&gt;0) {
            value end = size-1;
            variable Integer from=-1;
            variable Integer to=-1;
            for (index in 0..end) {
                if (exists elem=getFromFirst(index),
                        !trimming(elem)) {
                    from = index;
                    break;
                }
            }
            else {
                return [];
            }
            for (index in end..0) {
                if (exists elem=getFromFirst(index),
                        !trimming(elem)) {
                    to = index;
                    break;
                }
            }
            else {
                return [];
            }
            return this[from..to];
        }
        else {
            return [];
        }
    }
    
    &quot;Trim the elements satisfying the given [[predicate 
     function|trimming]], along with any null elements, from
     the start of this list, returning a list no longer than 
     this list.
     
     This is an eager operation.&quot;
    shared default 
    List&lt;Element&gt; trimLeading(
        &quot;The predicate function that the trimmed elements 
         satisfy.&quot;
        Boolean trimming(Element&amp;Object elem)) {
        if (size&gt;0) {
            value end = size-1;
            for (index in 0..end) {
                if (exists elem=getFromFirst(index),
                        !trimming(elem)) {
                    return this[index..end];
                }
            }
        }
        return [];
    }
    
    &quot;Trim the elements satisfying the given [[predicate 
     function|trimming]], along with any null elements, from 
     the end of this list, returning a list no longer than 
     this list.
     
     This is an eager operation.&quot;
    shared default 
    List&lt;Element&gt; trimTrailing(
        &quot;The predicate function that the trimmed elements 
         satisfy.&quot;
        Boolean trimming(Element&amp;Object elem)) {
        if (size&gt;0) {
            value end = size-1;
            for (index in end..0) {
                if (exists elem=getFromFirst(index),
                        !trimming(elem)) {
                    return this[0..index];
                }
            }
        }
        return [];
    }
    
    &quot;Return two lists, the first containing the elements
     that occur before the given [[index]], the second with
     the elements that occur after the given `index`. If the
     given `index` is outside the range of indexes of this
     list, one of the returned lists will be empty.
     
     For any `list`, and for any integer `index`:
     
         list.slice(index) == [list[...index-1], list[index...]]
     
     This is an eager operation.&quot;
    since(&quot;1.1.0&quot;)
    shared default 
    List&lt;Element&gt;[2] slice(Integer index)
            =&gt; [this[...index-1], this[index...]];
    
    &quot;Select the first elements of this list, returning a 
     list no longer than the given length. If this list is 
     shorter than the given length, return this list. 
     Otherwise return a list of the given length. If 
     `length&lt;=0` return an empty list.
     
     For any `list`, and for any integer `length`:
     
         list.initial(length) == list[...length-1] == list[0:length]
     
     This is an eager operation.&quot;
    see (`function terminal`, 
         `function sublistTo`,
         `function take`)
    shared default 
    List&lt;Element&gt; initial(Integer length)
            =&gt; this[...length-1];
    
    &quot;Select the last elements of the list, returning a list 
     no longer than the given length. If this list is 
     shorter than the given length, return this list. 
     Otherwise return a list of the given length.
     
     For any `list`, and for any integer `length`:
     
         list.terminal(length) == list[size-length...]
     
     This is an eager operation.&quot;
    see (`function initial`)
    shared default 
    List&lt;Element&gt; terminal(Integer length) 
            =&gt; this[size-length...];
    
    shared actual default 
    List&lt;Element&gt; span(Integer from, Integer to) {
        if (size&gt;0) {
            value end = size-1;
            if (from &lt;= to) {
                return 
                    if (to &gt;= 0 &amp;&amp; from &lt;= end) 
                    then ArraySequence(Array(sublist(from,to)))
                    else [];
            }
            else {
                return 
                    if (from &gt;= 0 &amp;&amp; to &lt;= end) 
                    then ArraySequence(Array(sublist(to,from).reversed))
                    else [];
            }
        }
        else {
            return [];
        }
    }
    
    shared actual default 
    List&lt;Element&gt; spanFrom(Integer from) {
        if (from&lt;=0) {
            return clone();
        }
        else if (from&lt;size) {
            return ArraySequence(Array(sublistFrom(from)));
        }
        else {
            return [];
        }
    }
    
    shared actual default 
    List&lt;Element&gt; spanTo(Integer to) {
        if (to&gt;=size-1) {
            return clone();
        }
        else if (to&gt;=0) {
            return ArraySequence(Array(sublistTo(to)));
        }
        else {
            return [];
        }
    }

    shared actual default
    List&lt;Element&gt; measure(Integer from, Integer length)
        =&gt; if (length &gt; 0)
        then span(from, from + length - 1)
        else [];

    &quot;A sequence containing the results of applying the given 
     mapping to the elements of this list.&quot;
    shared default actual 
    Result[] collect&lt;Result&gt;(
        &quot;The transformation applied to the elements.&quot;
        Result collecting(Element element)) {
        if (empty) {
            return [];
        }
        else {
            object list
                    extends Object() 
                    satisfies List&lt;Result&gt; {
                lastIndex =&gt; outer.lastIndex;
                size = outer.size;
                getFromFirst(Integer index)
                        =&gt; if (0&lt;=index&lt;size) 
                        then collecting(outer.getElement(index))
                        else null;
            }
            return ArraySequence(Array(list));
        }
    }
    
    class Indexes()
            extends Object()
            satisfies List&lt;Integer&gt; {
        
        lastIndex =&gt; outer.lastIndex;
        
        getFromFirst(Integer index)
                =&gt; defines(index) then index;
        
        clone() =&gt; 0:size;
        
        measure(Integer from, Integer length)
                =&gt; clone()[from:length];
        
        span(Integer from, Integer to)
                =&gt; clone()[from..to];
        
        spanFrom(Integer from) 
                =&gt; clone()[from...];
        
        spanTo(Integer to) 
                =&gt; clone()[...to];
        
        string =&gt; if (exists endIndex=lastIndex)
            then &quot;{ 0, ... , ``endIndex`` }&quot;
            else &quot;{}&quot;;
        
        iterator() 
                =&gt; object satisfies Iterator&lt;Integer&gt; {
                variable value i=0;
                next() =&gt; i&lt;size then i++ else finished;
                string =&gt; &quot;``outer.string``.iterator()&quot;;
            };
        
    }
    
    class Rest(Integer from)
            extends Object()
            satisfies List&lt;Element&gt; {
        
        assert (from&gt;=0);
        
        sublistFrom(Integer from)
                =&gt; if (from&gt;0) 
                then outer.Rest(from+this.from) 
                else this;
        
        getFromFirst(Integer index) 
                =&gt; if (index&lt;0)
                then null 
                else outer.getFromFirst(index+from);
        
        lastIndex 
                =&gt; let (size = outer.size-from) 
                    (size&gt;0 then size-1);
        
        measure(Integer from, Integer length) 
                =&gt; outer[from+this.from:length];
        
        span(Integer from, Integer to) 
                =&gt; outer[from+this.from..to+this.from];
        
        spanFrom(Integer from) 
                =&gt; outer[from+this.from...];
        
        spanTo(Integer to) 
                =&gt; outer[this.from..to+this.from];
        
        clone() =&gt; outer.clone().Rest(from);
        
        iterator() 
                =&gt; let (o = outer)
            object satisfies Iterator&lt;Element&gt; {
                variable value i=0;
                next() =&gt; if (i &lt; outer.size)
                    then o.getElement(from + i++)
                    else finished;
                string =&gt; &quot;``outer.string``.iterator()&quot;;
            };
        
    }
    
    class Sublist(Integer to)
            extends Object()
            satisfies List&lt;Element&gt; {
        
        assert (to&gt;=0);
        
        sublistTo(Integer to) 
                =&gt; if (to&lt;0) then [] 
                else if (to&lt;this.to) then outer.Sublist(to) 
                else this;
        
        getFromFirst(Integer index)
                =&gt; if (0&lt;=index&lt;=to)
                then outer.getFromFirst(index)
                else null;
        
        lastIndex 
                =&gt; let (endIndex = outer.size-1) 
                    (endIndex&gt;=0 then
                        (endIndex&lt;to then endIndex else to));
        
        measure(Integer from, Integer length) 
                =&gt; from+length-1&gt;to 
                    then outer[from:to] 
                    else outer[from:length];
        
        span(Integer from, Integer to) 
                =&gt; to&gt;this.to
                    then outer[from..this.to] 
                    else outer[from..to];
        
        spanFrom(Integer from) 
                =&gt; outer[from..to];
        
        spanTo(Integer to)
                =&gt; to&gt;this.to
                    then outer[...this.to] 
                    else outer[...to];
        
        clone() =&gt; outer.clone().Sublist(to);
        
        iterator() 
                =&gt; let (iter = outer.iterator()) 
            object satisfies Iterator&lt;Element&gt; {
                variable value i=0;
                next() =&gt; i++&gt;to 
                    then finished 
                    else iter.next();
                string =&gt; &quot;``outer.string``.iterator()&quot;;
            };
        
    }
            
    class Repeat(Integer times)
            extends Object()
            satisfies List&lt;Element&gt; {
        
        size =&gt; outer.size*times;
        
        lastIndex 
                =&gt; let (size = this.size) 
                    (size&gt;0 then size-1);
        
        getFromFirst(Integer index) 
                =&gt; let (size = outer.size) 
                    if (index&lt;size*times) 
                        then outer.getFromFirst(index%size)
                        else null;
        
        clone() =&gt; outer.clone().Repeat(times);
        
        iterator() =&gt; CycledIterator(outer,times);
        
        string =&gt; &quot;(``outer.string``).repeat(``times``)&quot;;
        
    }
    
    class Patch&lt;Other&gt;(List&lt;Other&gt; list, 
        Integer from, Integer length)
            extends Object()
            satisfies List&lt;Element|Other&gt; {
        
        assert (length&gt;=0);
        assert (0&lt;=from&lt;=outer.size);

        value exactLength =&gt; smallest(length, outer.size-from);
        
        size =&gt; outer.size+list.size-exactLength;
        
        lastIndex 
                =&gt; let (size = this.size) 
                    (size&gt;0 then size-1);
        
        getFromFirst(Integer index) 
                =&gt; if (index&lt;from) then
                    outer.getFromFirst(index)
                else if (index-from&lt;list.size) then
                    list.getFromFirst(index-from)
                else
                    outer.getFromFirst(index-list.size+exactLength);
        
        clone() =&gt; outer.clone().Patch(list.clone(),from,exactLength);
        
        iterator() 
                =&gt; let (iter = outer.iterator(), 
                        patchIter = list.iterator()) 
            object satisfies Iterator&lt;Element|Other&gt; {
                variable value index = -1;
                shared actual Element|Other|Finished next() {
                    if (++index==from) {
                        for (skip in 0:exactLength) {
                            iter.next();
                        }
                    }
                    return if (0&lt;=index-from&lt;list.size)
                        then patchIter.next()
                        else iter.next();
                }
                string =&gt; &quot;``outer.string``.iterator()&quot;;
            };
        
    }
    
    class Reversed()
            extends Object()
            satisfies List&lt;Element&gt; {
        
        lastIndex =&gt; outer.lastIndex;
        size =&gt; outer.size;
        first =&gt; outer.last;
        last =&gt; outer.first;
        
        reversed =&gt; outer;
        
        getFromFirst(Integer index)
                =&gt; if (size&gt;0)
                then outer.getFromFirst(size-1-index)
                else null;
        
        measure(Integer from, Integer length)
                =&gt; if (size&gt;0 &amp;&amp; length&gt;0)
                then let (start = size-1-from) 
                    outer[start..start-length+1]
                else [];
        
        span(Integer from, Integer to) 
                =&gt; outer[to..from];
        
        spanFrom(Integer from) 
                =&gt; let (endIndex = size-1) 
                    if (endIndex&gt;=0, from&lt;=endIndex) 
                        then outer[endIndex-from..0]
                        else [];
        
        spanTo(Integer to)
                =&gt; let (endIndex = size-1) 
                    if (endIndex&gt;=0 &amp;&amp; to&gt;=0)
                        then outer[endIndex..endIndex-to]
                        else [];
        
        clone() =&gt; outer.clone().reversed;
        
        iterator() 
                =&gt; let (outerList = outer) 
            object satisfies Iterator&lt;Element&gt; {
                variable value index=outerList.size-1;
                next() =&gt; index&lt;0 
                    then finished 
                    else outerList.getElement(index--);
                string =&gt; &quot;``outer.string``.iterator()&quot;;
            };
        
    }
    
    &quot;Produces a list with the same indexes as this list. For 
     every index, the element is the result of applying the 
     given [[transformation|List.mapElements.mapping]] 
     function to its associated element in this list. This 
     is a lazy operation, returning a view of this list.&quot;
    since(&quot;1.3.0&quot;)
    shared default 
    List&lt;Result&gt; mapElements&lt;Result&gt;(
        &quot;The function that transforms an index/item pair of
         this list, producing the element of the resulting 
         list.&quot;
        Result mapping(Integer index, Element item)) 
            =&gt; object
            extends Object()
            satisfies List&lt;Result&gt; {
        
        shared actual Result? getFromFirst(Integer index) {
            if (0 &lt;= index &lt; size) {
                if (exists element 
                        = outer.getFromFirst(index)) {
                    return mapping(index, element);
                }
                else {
                    assert (is Element null);
                    return mapping(index, null);
                }
            }
            else {
                return null;
            }
        }
        
        iterator() 
                =&gt; let (it = outer.iterator())
                object satisfies Iterator&lt;Result&gt; {
                    variable value index = 0;
                    next() =&gt; if (!is Finished element 
                                    = it.next()) 
                                then mapping(index++, element)
                                else finished;
                };
        
        lastIndex =&gt; outer.lastIndex;
        size =&gt; outer.size;
        
        clone() =&gt; outer.clone().mapElements(mapping);
        
    };
    
}
</pre></body></html>