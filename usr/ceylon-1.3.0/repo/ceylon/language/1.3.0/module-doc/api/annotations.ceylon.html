<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>annotations.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>import ceylon.language.meta.declaration {
    Module,
    Package,
    Import,
    ClassDeclaration,
    ClassOrInterfaceDeclaration,
    ConstructorDeclaration,
    FunctionDeclaration,
    Declaration,
    ValueDeclaration
}

&quot;The annotation class for the [[annotation]] meta-annotation.&quot;
shared final sealed annotation class AnnotationAnnotation()
        satisfies OptionalAnnotation&lt;AnnotationAnnotation,
                ClassDeclaration|FunctionDeclaration&gt; {}

&quot;Annotation to mark a class as an *annotation class*, or a 
 top-level function as an *annotation constructor*.&quot;
see (`interface Annotation`)
shared annotation AnnotationAnnotation annotation()
        =&gt; AnnotationAnnotation();

&quot;The annotation class for the [[shared]] annotation.&quot;
shared final sealed annotation class SharedAnnotation()
        satisfies OptionalAnnotation&lt;SharedAnnotation,
                FunctionDeclaration|ValueDeclaration|ClassOrInterfaceDeclaration|ConstructorDeclaration|Package|Import&gt; {}

&quot;Annotation to mark a declaration as shared. A `shared` 
 declaration is visible outside the block of code in which 
 it is declared.&quot;
shared annotation SharedAnnotation shared()
        =&gt; SharedAnnotation();

&quot;The annotation class for the [[variable]] annotation.&quot;
shared final sealed annotation class VariableAnnotation()
        satisfies OptionalAnnotation&lt;VariableAnnotation,
                ValueDeclaration&gt; {}

&quot;Annotation to mark a value as variable. A `variable` value 
 may be assigned multiple times.&quot;
shared annotation VariableAnnotation variable()
        =&gt; VariableAnnotation();

&quot;The annotation class for the [[abstract]] annotation.&quot;
shared final sealed annotation class AbstractAnnotation()
        satisfies OptionalAnnotation&lt;AbstractAnnotation,
                ClassDeclaration|ConstructorDeclaration&gt; {}

&quot;Annotation to mark a class as abstract. An `abstract` class
 may have `formal` members, but may not be directly 
 instantiated. An enumerated class must be `abstract`.&quot;
shared annotation AbstractAnnotation abstract()
        =&gt; AbstractAnnotation();

&quot;The annotation class for the [[final]] annotation.&quot;
shared final sealed annotation class FinalAnnotation()
        satisfies OptionalAnnotation&lt;FinalAnnotation,
                ClassDeclaration&gt; {}

&quot;Annotation to mark a class as final. A `final` class may 
 not be extended. Marking a class as `final` affects disjoint
 type analysis.&quot;
shared annotation FinalAnnotation final()
        =&gt; FinalAnnotation();
                
&quot;The annotation class for the [[sealed]] annotation.&quot;
since(&quot;1.1.0&quot;)
shared final sealed annotation class SealedAnnotation()
        satisfies OptionalAnnotation&lt;SealedAnnotation,
                ClassOrInterfaceDeclaration|ConstructorDeclaration&gt; {}
                
&quot;Annotation to mark an interface, class, or constructor as 
 sealed. A `sealed` interface may not be satisfied outside 
 of the module in which it is defined. A `sealed` class may 
 not be extended or instantiated outside of the module in 
 which it is defined. A `sealed` constructor may not be
 invoked outside of the module in which it is defined.&quot;
since(&quot;1.1.0&quot;)
shared annotation SealedAnnotation sealed()
        =&gt; SealedAnnotation();

&quot;The annotation class for the [[actual]] annotation.&quot;
shared final sealed annotation class ActualAnnotation()
        satisfies OptionalAnnotation&lt;ActualAnnotation,
                FunctionDeclaration|ValueDeclaration|ClassOrInterfaceDeclaration&gt; {}

&quot;Annotation to mark a member of a type as refining a member 
 of a supertype.&quot;
shared annotation ActualAnnotation actual()
        =&gt; ActualAnnotation();

&quot;The annotation class for the [[formal]] annotation.&quot;
shared final sealed annotation class FormalAnnotation()
        satisfies OptionalAnnotation&lt;FormalAnnotation,
                FunctionDeclaration|ValueDeclaration|ClassOrInterfaceDeclaration&gt; {}

&quot;Annotation to mark a member whose implementation must be 
 provided by subtypes.&quot;
shared annotation FormalAnnotation formal()
        =&gt; FormalAnnotation();

&quot;The annotation class for the [[default]] annotation.&quot;
shared final sealed annotation class DefaultAnnotation()
        satisfies OptionalAnnotation&lt;DefaultAnnotation,
                FunctionDeclaration|ValueDeclaration|ClassOrInterfaceDeclaration&gt; {}

&quot;Annotation to mark a member whose implementation may be 
 refined by subtypes. Non-`default` declarations may not be 
 refined.&quot;
shared annotation DefaultAnnotation default()
        =&gt; DefaultAnnotation();

&quot;The annotation class for the [[late]] annotation.&quot;
shared final sealed annotation class LateAnnotation()
        satisfies OptionalAnnotation&lt;LateAnnotation,
                ValueDeclaration&gt; {}

&quot;Annotation to disable definite initialization analysis for 
 a reference.&quot;
shared annotation LateAnnotation late()
        =&gt; LateAnnotation();

&quot;The annotation class for the [[native]] annotation.&quot;
shared final sealed annotation class NativeAnnotation(backends)
        satisfies OptionalAnnotation&lt;NativeAnnotation,Annotated&gt; {
    &quot;The compiler backend that this native annotation applies to,
     or the empty string to declare the annotated element is a
     native header.&quot;
    since(&quot;1.2.0&quot;)
    shared String* backends;
}

&quot;Annotation to mark a member whose implementation is defined 
 in platform-native code.&quot;
shared annotation NativeAnnotation native(
    since(&quot;1.2.0&quot;)
    String* backends)
        =&gt; NativeAnnotation(*backends);

/*&quot;The annotation class for [[inherited]].&quot;
shared final sealed annotation class InheritedAnnotation()
        satisfies OptionalAnnotation&lt;InheritedAnnotation, 
            ClassDeclaration&gt; {}

&quot;Annotation to mark an annotation class as a *inherited*.&quot;
shared annotation InheritedAnnotation inherited() 
        =&gt; InheritedAnnotation();*/

&quot;The annotation class for the [[doc]] annotation.&quot;
shared final sealed annotation class DocAnnotation(
    &quot;Documentation, in Markdown syntax, about the annotated 
     program element&quot;
    shared String description)
        satisfies OptionalAnnotation&lt;DocAnnotation,Annotated&gt; {}

&quot;Annotation to specify API documentation of a program
 element.&quot;
shared annotation DocAnnotation doc(
    &quot;Documentation, in Markdown syntax, about the annotated element&quot;
    String description) =&gt; DocAnnotation(description);

&quot;The annotation class for the [[see]] annotation.&quot;
shared final sealed annotation class SeeAnnotation(
    &quot;The program elements being referred to.&quot;
    shared Declaration* programElements)
        satisfies SequencedAnnotation&lt;SeeAnnotation,Annotated&gt; {}

&quot;Annotation to specify references to other program elements
 related to the annotated API.&quot;
shared annotation SeeAnnotation see(
    &quot;The program elements being referred to.&quot;
    Declaration* programElements)
        =&gt; SeeAnnotation(*programElements);

&quot;The annotation class for the [[by]] annotation.&quot;
shared final sealed annotation class AuthorsAnnotation(
    &quot;The authors, in Markdown syntax, of the annotated 
     program element&quot;
    shared String* authors)
        satisfies OptionalAnnotation&lt;AuthorsAnnotation,Annotated&gt; {}

&quot;Annotation to document the authors of an API.&quot;
shared annotation AuthorsAnnotation by(
    &quot;The authors, in Markdown syntax, of the annotated 
     program element&quot;
    String* authors)
        =&gt; AuthorsAnnotation(*authors);

&quot;The annotation class for the [[throws]] annotation.&quot;
shared final sealed annotation class ThrownExceptionAnnotation(
    &quot;The [[Exception]] type that is thrown.&quot;
    shared Declaration type,
    &quot;A description, in Markdown syntax, of the circumstances 
     that cause this exception to be thrown.&quot;
    shared String when)
        satisfies SequencedAnnotation&lt;ThrownExceptionAnnotation,
                FunctionDeclaration|ValueDeclaration|ClassDeclaration|ConstructorDeclaration&gt; {}

&quot;Annotation to document the exception types thrown by a 
 function, value, class, or constructor.&quot;
shared annotation ThrownExceptionAnnotation throws(
    &quot;The [[Exception]] type that is thrown.&quot;
    Declaration type,
    &quot;A description, in Markdown syntax, of the circumstances 
     that cause this exception to be thrown.&quot;
    String when = &quot;&quot;)
        =&gt; ThrownExceptionAnnotation(type, when);

&quot;The annotation class for the [[deprecated]] annotation.&quot;
shared final sealed annotation class DeprecationAnnotation(
    &quot;A description, in Markdown syntax, of why the program 
     element is deprecated, and of what alternatives are 
     available.&quot;
    shared String description)
        satisfies OptionalAnnotation&lt;DeprecationAnnotation,
                Annotated&gt; {
    &quot;A description, in Markdown syntax, of why the program 
     element is deprecated, and what alternatives are 
     available, or `null`.&quot;
    shared String? reason
            =&gt; !description.empty then description;
}

&quot;Annotation to mark program elements which should not be 
 used anymore.&quot;
shared annotation DeprecationAnnotation deprecated(
    &quot;A description, in Markdown syntax, of why the program 
     element is deprecated, and what alternatives are 
     available.&quot;
    String reason = &quot;&quot;)
        =&gt; DeprecationAnnotation(reason);

&quot;The annotation class for the [[tagged]] annotation.&quot;
shared final sealed annotation class TagsAnnotation(
    &quot;The tags, in plain text.&quot;
    shared String* tags)
        satisfies OptionalAnnotation&lt;TagsAnnotation,Annotated&gt; {}

&quot;Annotation to categorize an API by tag.&quot;
shared annotation TagsAnnotation tagged(
    &quot;The tags, in plain text.&quot;
    String* tags)
        =&gt; TagsAnnotation(*tags);

&quot;The annotation class for the [[aliased]] annotation.&quot;
since(&quot;1.2.0&quot;)
shared final sealed annotation class AliasesAnnotation(
    &quot;The aliases, in plain text.&quot;
    shared String* aliases)
        satisfies OptionalAnnotation&lt;AliasesAnnotation,Annotated&gt; {}

&quot;Annotation to specify a list of aliases that tools such as auto-completion and
 quick-fixes should consider, to help users find a declaration using its aliases.&quot;
since(&quot;1.2.0&quot;)
shared annotation AliasesAnnotation aliased(
    &quot;The aliases, in plain text.&quot;
    String* aliases)
        =&gt; AliasesAnnotation(*aliases);

&quot;The annotation class for the [[license]] annotation.&quot;
shared final sealed annotation class LicenseAnnotation(
    &quot;The name, text, or URL of the license.&quot;
    shared String description)
        satisfies OptionalAnnotation&lt;LicenseAnnotation,Module&gt; {}

&quot;Annotation to specify the URL of the license of a module or 
 package.&quot;
shared annotation LicenseAnnotation license(
    &quot;The name, text, or URL of the license.&quot;
    String description)
        =&gt; LicenseAnnotation(description);

&quot;The annotation class for the [[optional]] annotation.&quot;
shared final sealed annotation class OptionalImportAnnotation()
        satisfies OptionalAnnotation&lt;OptionalImportAnnotation,
                Import&gt; {}

&quot;Annotation to specify that a module can be executed even if 
 the annotated dependency is not available.&quot;
shared annotation OptionalImportAnnotation optional()
        =&gt; OptionalImportAnnotation();

&quot;The annotation class for the [[suppressWarnings]] 
 annotation.&quot;
since(&quot;1.2.0&quot;)
shared final sealed annotation class SuppressWarningsAnnotation(
    &quot;The warning types to suppress.&quot;
    shared String* warnings)
        satisfies OptionalAnnotation&lt;SuppressWarningsAnnotation, 
            FunctionDeclaration|ValueDeclaration|ClassOrInterfaceDeclaration|ConstructorDeclaration|Module|Package|Import&gt; {}

&quot;Annotation to suppress compilation warnings of the 
 [[specified types|warnings]] when typechecking the 
 annotated program element.&quot;
since(&quot;1.2.0&quot;)
shared annotation SuppressWarningsAnnotation suppressWarnings(
    &quot;The warning types to suppress.
     
     Allowed warning types are:
     `filenameNonAscii`,
     `filenameCaselessCollision`,
     `deprecation`, 
     `disjointEquals`,
     `disjointContainment`,
     `compilerAnnotation`,
     `doclink`,
     `expressionTypeNothing`,
     `expressionTypeCallable`,
     `unusedDeclaration`,
     `unusedImport`,
     `ceylonNamespace`,
     `javaNamespace,` 
     `suppressedAlready`, 
     `suppressesNothing`, 
     `unknownWarning`, 
     `ambiguousAnnotation`,
     `javaAnnotationElement`,
     `syntaxDeprecation`,
     `smallIgnored`,
     `literalNotSmall`,
     `disjointEquals`,
     `disjointContainment`,
     `redundantNarrowing`,
     `redundantIteration`,
     `missingImportPrefix`,
     `uncheckedTypeArguments`.&quot;
    String* warnings) 
        =&gt; SuppressWarningsAnnotation(*warnings);

&quot;The annotation class for the [[serializable]] annotation.&quot;
since(&quot;1.2.0&quot;)
shared final annotation class SerializableAnnotation()
        satisfies OptionalAnnotation&lt;SerializableAnnotation,ClassDeclaration&gt; {
}

&quot;Annotation to specify that a class is serializable.
  
 A serializable class may have instances that cannot be 
 serialized if those instances have reachable references to 
 instances of non-serializable classes.&quot;
since(&quot;1.2.0&quot;)
shared annotation SerializableAnnotation serializable() 
        =&gt; SerializableAnnotation();


&quot;The annotation class for the [[small]] annotation.&quot;
since(&quot;1.3.0&quot;)
shared final annotation class SmallAnnotation()
        satisfies OptionalAnnotation&lt;SmallAnnotation,ValueDeclaration|FunctionDeclaration&gt; {
}

&quot;Annotation to hint to the compiler that an `Integer` or 
 `Float` typed value or function should be represented using 
 a 32-bit signed integer or 32-bit IEEE float if possible.&quot;
since(&quot;1.3.0&quot;)
shared annotation SmallAnnotation small() 
        =&gt; SmallAnnotation();


&quot;The annotation class for the [[service]] annotation.&quot;
since(&quot;1.3.0&quot;)
shared final annotation class ServiceAnnotation(contract)
        satisfies SequencedAnnotation&lt;ServiceAnnotation,ClassDeclaration&gt; {
    &quot;The service interface or class that the annotated class 
     provides.&quot;
    shared ClassOrInterfaceDeclaration contract;
}

&quot;Annotation marking a class as implementing a service. The
 class must be a non-abstract, shared, toplevel class.
 
 For example, if `Manager` is an interface, this code
 declares an implementation of `Manager`:
 
     service (`Manager`)
     shared class DefautManager() satisfies Manager {}
 
 Service implementations can be found at runtime using 
 [[ceylon.language.meta.declaration::Module.findServiceProviders]].
 
     {Manager*} managers = `module`.findServiceProviders(`Manager`);
     assert (exists manager = managers.first);&quot;
since(&quot;1.3.0&quot;)
shared annotation ServiceAnnotation service(
    &quot;The service interface or class that the annotated class 
     provides.&quot;
    ClassOrInterfaceDeclaration contract) 
        =&gt; ServiceAnnotation(contract);

&quot;The annotation class for the [[since]] annotation.&quot;
since(&quot;1.3.0&quot;)
shared final sealed annotation class SinceAnnotation(
    &quot;The version of the module when this declaration was added.&quot;
    shared String version)
        satisfies OptionalAnnotation&lt;SinceAnnotation,Annotated&gt; {}

&quot;Annotation to indicate at which moment the annotated declaration
 was added to the module.&quot;
since(&quot;1.3.0&quot;)
shared annotation SinceAnnotation since(
    &quot;The version of the module when this declaration was added.&quot;
    String version) 
        =&gt; SinceAnnotation(version);
</pre></body></html>