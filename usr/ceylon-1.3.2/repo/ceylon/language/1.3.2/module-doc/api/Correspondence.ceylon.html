<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Correspondence.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='.resources/ceylondoc.css' rel='stylesheet' type='text/css'/>
<link href='//fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/rainbow.linenumbers.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;Abstract supertype of objects which associate values with 
 keys. A `Correspondence&lt;Key,Item&gt;` may be a viewed as a 
 partial function from domain `Key` to range `Item`, where
 some `Key`s have no `Item`.
 
 `Correspondence` does not satisfy [[Category]], since in 
 some cases&amp;mdash;`List`, for example&amp;mdash;it is convenient 
 to consider the subtype a `Category` of its indexed items, 
 and in other cases&amp;mdash;`Map`, for example&amp;mdash;it is 
 convenient to treat the subtype as a `Category` of its 
 [[entries|Entry]].
 
 The item corresponding to a given key may be obtained from 
 a `Correspondence` using the item operator:
 
     value bg = settings[\&quot;backgroundColor\&quot;] else white;
 
 The `get()` operation and item operator result in an
 optional type, to reflect the possibility that there may be
 no item for the given key.&quot;
see (`interface Map`, 
     `interface List`, 
     `interface Category`)
by (&quot;Gavin&quot;)
tagged(&quot;Collections&quot;)
shared interface Correspondence&lt;in Key, out Item=Anything&gt;
        given Key satisfies Object {
    
    &quot;Returns the value defined for the given key, or `null` 
     if there is no value defined for the given key.
     
     For any instance `c` of `Correspondence`, `c.get(key)` 
     may be written using the item operator:
     
         c[key]&quot;
    see (`function Correspondence.getAll`)
    shared formal Item? get(Key key);
    
    &quot;Determines if there is a value defined for the given 
     key.&quot;
    see (`function Correspondence.definesAny`, 
         `function Correspondence.definesEvery`, 
         `value Correspondence.keys`)
    shared formal Boolean defines(Key key);
    
    /*&quot;Return a boolean value indicating whether there is an
     item with the given [[key]], together with the 
     [[item|Item]] for that key, if any.
     
         value found -&gt; item = map.lookup(key);
         if (found) {
             print(item);
         }
     
     This operation is useful for correspondences which 
     contain [[null]] items, where a null return value for 
     [[get]] does not distinguish between:
     
     - the case that there is no item for the given key, and
     - the case that `null` is the item for the given key.&quot;
    shared default
    see (`function defines`, `function get`)
    Boolean-&gt;Item? lookup(Key key)
            =&gt; if (defines(key))
               then true -&gt; get(key)
               else false -&gt; null;*/
    
    &quot;The `Category` of all keys for which a value is defined
     by this `Correspondence`.&quot;
    see (`function Correspondence.defines`)
    shared default Category&lt;Key&gt; keys
            =&gt; object 
            satisfies Category&lt;Key&gt; {
        contains(Key key) =&gt; defines(key);
    };
    
    &quot;Determines if this `Correspondence` defines a value for
     every one of the given keys.&quot;
    see (`function Correspondence.defines`)
    shared default 
    Boolean definesEvery({Key*} keys) {
        for (key in keys) {
            if (!defines(key)) {
                return false;
            }
        }
        else {
            return true;
        }
    }
    
    &quot;Determines if this `Correspondence` defines a value for
     any one of the given keys.&quot;
    see (`function Correspondence.defines`)
    shared default 
    Boolean definesAny({Key*} keys) {
        for (key in keys) {
            if (defines(key)) {
                return true;
            }
        }
        else {
            return false;
        }
    }
    
    &quot;Returns the items defined for the given keys, in the 
     same order as the corresponding keys. For any key which 
     does not have an item defined, the resulting stream 
     contains the value `null`.&quot;
    see (`function Correspondence.get`)
    since(&quot;1.1.0&quot;)
    shared default 
    Iterable&lt;Item?,Absent&gt; getAll&lt;Absent&gt;
            (Iterable&lt;Key,Absent&gt; keys) 
            given Absent satisfies Null
            =&gt; { for (key in keys) get(key) };
    
}

&quot;A [[Correspondence]] that supports mutation of its 
 constituent key/item associations. Items may be mutated
 via the assignment and item operators:
 
     array[i] = i^2;
 
 Every `CorrespondenceMutator` is either:
  
 - a [[KeyedCorrespondenceMutator]], which allows the 
   creation of new key/item associations, or 
 - an [[IndexedCorrespondenceMutator]], which only allows
   mutation of items associated with a given range of 
   integer indexes.
 
 Most `CorrespondenceMutator`s are also instances of
 [[Correspondence]].&quot;
since(&quot;1.3.0&quot;)
tagged(&quot;Collections&quot;)
see (`interface Correspondence`, 
     `interface KeyedCorrespondenceMutator`,
     `interface IndexedCorrespondenceMutator`)
shared interface CorrespondenceMutator&lt;in Item&gt;
        of IndexedCorrespondenceMutator&lt;Item&gt;
         | KeyedCorrespondenceMutator&lt;Nothing,Item&gt; {}

&quot;A [[CorrespondenceMutator]] which allows mutation of the
 item associated with a given integer index from a range
 of adjacent indices.
 
 Many `IndexedCorrespondenceMutator`s are [[List]]s.&quot;
since(&quot;1.3.0&quot;)
tagged(&quot;Collections&quot;)
see (`interface KeyedCorrespondenceMutator`)
shared interface IndexedCorrespondenceMutator&lt;in Element&gt; 
        satisfies CorrespondenceMutator&lt;Element&gt; {

    &quot;Set the item associated with the given [[index]] to the
     given [[item]], replacing the item previously 
     associated with this index.
     
     For any instance `c` of `IndexedCorrespondenceMutator`, 
     `c.set(index, item)` may be written using the item and 
     assignment operators:
     
         c[index] = item&quot;
    throws (`class AssertionError`,
            &quot;if the given [[index]] is outside the range of
             indexes belonging to this objects&quot;)
    shared formal void set(Integer index, Element item);
}

&quot;A [[CorrespondenceMutator]] which allows mutation of the
 item associated with an existing key, and creation of a
 new key/item association.
 
 Many `KeyedCorrespondenceMutator`s are [[Map]]s.&quot;
since(&quot;1.3.0&quot;)
tagged(&quot;Collections&quot;)
see (`interface IndexedCorrespondenceMutator`)
shared interface KeyedCorrespondenceMutator&lt;in Key, in Item&gt;
        satisfies CorrespondenceMutator&lt;Item&gt;
        given Key satisfies Object {

    &quot;Set the item associated with the given [[key]] to the
     given [[item]]. If there is already an item associated
     with this key, replace the association. Otherwise, 
     create a new association.
     
     For any instance `c` of `KeyedCorrespondenceMutator`, 
     `c.put(key, item)` may be written using the item and
     assignment operators:
     
         c[key] = item&quot;
    shared formal void put(Key key, Item item);
}
</pre></body></html>