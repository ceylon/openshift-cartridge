<!DOCTYPE html><html xmlns='http://www.w3.org/1999/xhtml'><head><meta charset='UTF-8'/>
<title>Map.ceylon</title><link href='.resources/favicon.ico' rel='shortcut icon'/>
<link href='.resources/ceylon.css' rel='stylesheet' type='text/css'/>
<link href='http://fonts.googleapis.com/css?family=Inconsolata' rel='stylesheet' type='text/css'/>
<script type='text/javascript'>var resourceBaseUrl = '.resources/'</script><script src='.resources/jquery-1.8.2.min.js' type='text/javascript'></script><script src='.resources/rainbow.min.js' type='text/javascript'></script><script src='.resources/ceylon.js' type='text/javascript'></script><script src='.resources/ceylondoc.js' type='text/javascript'></script></head><body><pre data-language='ceylon' style='font-family: Inconsolata, Monaco, Courier, monospace'>&quot;&quot;&quot;Represents a collection which maps _keys_ to _items_,
   where a key can map to at most one item. Each such 
   mapping may be represented by an [[Entry]].
   
   A `Map` is a [[Collection]] of its `Entry`s, and a 
   [[Correspondence]] from keys to items.
   
   The presence of an entry in a map may be tested using the 
   `in` operator:
   
       if (&quot;lang&quot;-&gt;&quot;en_AU&quot; in settings) { ... }
 
   The entries of the map may be iterated using `for`:
   
       for (key-&gt;item in settings) { ... }
   
   The item for a key may be obtained using the item
   operator:
   
       String lang = settings[&quot;lang&quot;] else &quot;en_US&quot;;
   
   Keys are compared for equality using [[Object.equals]] or
   [[Comparable.compare]]. There may be at most one entry 
   per key.&quot;&quot;&quot;
see (`class Entry`, 
     `function forKey`, `function forItem`, 
     `function byItem`, `function byKey`)
shared interface Map&lt;out Key,out Item&gt;
        satisfies Collection&lt;Key-&gt;Item&gt; &amp;
                  Correspondence&lt;Object,Item&gt;
        given Key satisfies Object {
    
    &quot;Determines if there is an entry in this map with the
     given [[key]].&quot;
    see (`function contains`)
    shared actual formal Boolean defines(Object key);
    
    &quot;Returns the item of the entry with the given [[key]], 
     or `null` if there is no entry with the given `key` in
     this map.&quot;
    shared actual formal Item? get(Object key);
    
    &quot;Determines if the given [[value|entry]] is an [[Entry]]
     belonging to this map.&quot;
    see (`function defines`)
    shared actual default Boolean contains(Object entry) {
        if (is Key-&gt;Anything entry, defines(entry.key)) {
            if (exists item = get(entry.key)) {
                if (exists entryItem = entry.item) {
                    return item==entryItem;
                }
                else {
                    return false;
                }
            }
            else {
                return !entry.item exists;
            }
        }
        else {
            return false;
        }
    }
    
    shared actual formal Map&lt;Key,Item&gt; clone();
    
    &quot;Two maps are considered equal iff they have the same 
     _entry sets_. The entry set of a `Map` is the set of 
     `Entry`s belonging to the map. Therefore, the maps are 
     equal iff they have same set of `keys`, and for every 
     key in the key set, the maps have equal items.&quot;
    shared actual default Boolean equals(Object that) {
        if (is Map&lt;Object,Anything&gt; that,
                that.size==size) {
            for (entry in this) {
                value thatItem = that[entry.key];
                if (exists thisItem = entry.item) {
                    if (exists thatItem) {
                        if (thatItem!=thisItem) {
                            return false;
                        }
                    }
                    else {
                        return false;
                    }
                }
                else if (thatItem exists) {
                    return false;
                }
            }
            else {
                return true;
            }
        }
        else {
            return false;
        }
    }
    
    shared actual default Integer hash {
        variable Integer hashCode = 0;
        for (elem in this) {
            hashCode += elem.hash;
        }
        return hashCode;
    }
    
    &quot;A [[Collection]] containing the keys of this map.&quot;
    actual shared default Collection&lt;Key&gt; keys {
        object keys
                satisfies Collection&lt;Key&gt; {
            contains(Object key) =&gt; outer.defines(key);
            iterator() =&gt; { for (k-&gt;v in outer) k }.iterator();
            clone() =&gt; [*this];
            size =&gt; outer.size;
        }
        return keys;
    }
    
    &quot;A [[Collection]] containing the items stored in this 
     map. An element can be stored under more than one key 
     in the map, and so it can occur more than once in the 
     resulting collection.&quot;
    shared default Collection&lt;Item&gt; items {
        object items
                satisfies Collection&lt;Item&gt; {
            shared actual Boolean contains(Object item) {
                for (k-&gt;v in outer) {
                    if (exists v, v==item) {
                        return true;
                    }
                }
                return false;
            }
            iterator() =&gt; { for (k-&gt;v in outer) v }.iterator();
            clone() =&gt; [*this];
            size =&gt; outer.size;
        }
        return items;
    }
    
    &quot;Produces a map with the same [[keys]] as this map. 
     For every key, the item is the result of applying the 
     given [[transformation|Map.mapItems.mapping]] function 
     to its associated item in this map. This is a lazy 
     operation, returning a view of this map.&quot;
    shared default Map&lt;Key,Result&gt; mapItems&lt;Result&gt;(
        &quot;The function that transforms a key/item pair of
         this map, producing the item of the resulting map.&quot;
        Result mapping(Key key, Item item)) 
            given Result satisfies Object {
        object map
                extends Object()
                satisfies Map&lt;Key,Result&gt; {
            
            defines(Object key) =&gt; outer.defines(key);
            
            shared actual Result? get(Object key) {
                if (is Key key, defines(key)) {
                    assert (is Item item = outer[key]);
                    return mapping(key, item);
                }
                else {
                    return null;
                }
            }
            
            function mapEntry(Key-&gt;Item entry) 
                    =&gt; entry.key -&gt; 
                        mapping(entry.key, entry.item);
            
            iterator() =&gt; outer.map(mapEntry).iterator();
            
            size =&gt; outer.size;
            
            clone() =&gt; outer.clone().mapItems(mapping);
            
        }
        return map;
    }
    
    &quot;Produces a map by applying a [[filtering]] function 
     to the [[keys]] of this map. This is a lazy operation, 
     returning a view of this map.&quot;
    shared default Map&lt;Key,Item&gt; filterKeys(
        &quot;The predicate function that filters the keys of 
         this map, determining if there is a corresponding
         entry in the resulting map.&quot;
        Boolean filtering(Key key)) {
        object map
                extends Object()
                satisfies Map&lt;Key,Item&gt; {
            
            shared actual Item? get(Object key) {
                if (is Key key, filtering(key)) {
                    return outer[key];
                }
                else {
                    return null;
                }
            }
            
            shared actual Boolean defines(Object key) {
                if (is Key key, filtering(key)) {
                    return outer.defines(key);
                }
                else {
                    return false;
                }
            }
            
            function filterEntry(Key-&gt;Item entry) 
                    =&gt; filtering(entry.key);
            
            iterator() =&gt; outer.filter(filterEntry).iterator();
            
            clone() =&gt; outer.clone().filterKeys(filtering);
            
        }
        return map;
    }
    
    &quot;Produces a map whose keys are the union of the keys
     of this map, with the keys of the given [[map|other]].
     For any given key in the resulting map, its associated
     item is the item associated with the key in the given
     map, if any, or the item associated with the key in 
     this map otherwise.
     
     That is, for any `key` in the resulting patched map:
     
         map.patch(other)[key] == other[key] else map[key]
     
     This is a lazy operation producing a view of this map
     and the given map.&quot;
    shared default
    Map&lt;Key|OtherKey,Item|OtherItem&gt; patch&lt;OtherKey,OtherItem&gt;
            (Map&lt;OtherKey,OtherItem&gt; other) 
            given OtherKey satisfies Object 
            given OtherItem satisfies Object {
        object patch 
                extends Object()
                satisfies Map&lt;Key|OtherKey,Item|OtherItem&gt; {
            
            get(Object key) =&gt; other[key] else outer[key];
            
            clone() =&gt; outer.clone().patch(other.clone());
            
            defines(Object key) 
                    =&gt; other.defines(key) || 
                       outer.defines(key);
            
            shared actual Boolean contains(Object entry) {
                if (is Entry&lt;Object,Object&gt; entry) {
                    return entry in other || 
                            !other.defines(entry.key) 
                                &amp;&amp; entry in outer;
                }
                else {
                    return false;
                }
            }
            
            //efficient when map is much smaller than outer,
            //which is probably the common case 
            size =&gt; outer.size +
                    other.keys.count(not(outer.defines));
            
            iterator() =&gt; ChainedIterator(other,
                outer.filter(not(other.contains)));
            
        }
        return patch;
    }
    
}

&quot;An immutable [[Map]] with no entries.&quot;
shared object emptyMap 
        extends Object() 
        satisfies Map&lt;Nothing, Nothing&gt; {
    
    shared actual Null get(Object key) =&gt; null;
    shared actual Collection&lt;Nothing&gt; keys
            =&gt; emptySet;
    shared actual Collection&lt;Nothing&gt; items
            =&gt; emptySet;
    
    clone() =&gt; this;
    iterator() =&gt; emptyIterator;
    size =&gt; 0;
    empty =&gt; true;
    
    defines(Object index) =&gt; false;
    
    contains(Object element) =&gt; false;
    containsAny({Object*} elements) =&gt; false;
    containsEvery({Object*} elements) =&gt; false;
    
    shared actual Map&lt;Nothing, Nothing&gt; mapItems&lt;Result&gt;
            (Result mapping(Nothing key, Nothing item))
            given Result satisfies Object 
            =&gt; emptyMap;
    
    shared actual Integer count
            (Boolean selecting(Nothing-&gt;Nothing element)) 
            =&gt; 0;
    
    shared actual Null find
            (Boolean selecting(Nothing-&gt;Nothing element)) 
            =&gt; null;
    
    shared actual Null findLast
            (Boolean selecting(Nothing-&gt;Nothing element)) 
            =&gt; null;
    
    shared actual Boolean any
            (Boolean selecting(Nothing-&gt;Nothing element)) 
            =&gt; false;
    
    shared actual Boolean every
            (Boolean selecting(Nothing-&gt;Nothing element)) 
            =&gt; true;
    
    skip(Integer skipping) =&gt; this;
    take(Integer taking) =&gt; this;
    by(Integer step) =&gt; this;
    
}
</pre></body></html>